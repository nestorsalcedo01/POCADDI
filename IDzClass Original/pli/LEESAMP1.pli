  /**
 * PLI Stored Procedure ENGLAND.LEESAMP
 * @param Action
 * @param City
 * @param Country
 * @param Response
 */
 LEESAMP1 : Procedure (
     Action,
      City,
      Country,
      Response     ) options (main);
 dcl Action CHAR (8) VAR;
 dcl City CHAR (32) VAR;
 dcl Country CHAR (32) VAR;
 dcl Response CHAR (80) VAR;
 dcl Zip Char(5) VAR;

  /*
 * PLI Stored Procedure Demo Include
 */

 dcl Sqlcode_Pic PIC'SSS999';


 DCL 1 SQLCAP,
      2 SQLCAPID      CHAR(8),
      2 SQLCAPBC      BIN FIXED(31),
      2 SQLCODE      BIN FIXED(31),
      2 SQLERRM      CHAR(70) VAR,
      2 SQLERRP      CHAR(8),
      2 SQLERRD(5)   BIN FIXED(31),
      2 SQLWARN,
        3 SQLWARN0   CHAR(1),
        3 SQLWARN1   CHAR(1),
        3 SQLWARN2   CHAR(1),
        3 SQLWARN3   CHAR(1),
        3 SQLWARN4   CHAR(1),
        3 SQLWARN5   CHAR(1),
        3 SQLWARN6   CHAR(1),
        3 SQLWARN7   CHAR(1),
        3 SQLWARN8   CHAR(1),
        3 SQLWARN9   CHAR(1),
      2 SQLSTATE     CHAR(5);

 Upper: Proc(In) returns(CHAR (8) VAR);
   dcl In CHAR (8) VAR;
   return (TRANSLATE(In,'ABCDEFGHIJKLMNOPQRSTUVWXYZ',
                        'abcdefghijklmnopqrstuvwxyz'));
 end;

 Action = Upper(Action);
 select (Action);
   when ('S','SELECT') do;
     Select;
       When (City='' & Country='') do;
         EXEC SQL
           DECLARE C1 CURSOR WITH RETURN WITH HOLD FOR
             SELECT * FROM ENGLAND.CITYTABLE
         ;
         EXEC SQL
           OPEN C1
         ;
       end;
       When (City='') do;
         EXEC SQL
           DECLARE C2 CURSOR WITH RETURN WITH HOLD FOR
             SELECT * FROM 
               ENGLAND.CITYTABLE
               WHERE COUNTRY=:COUNTRY
         ;
         EXEC SQL
           OPEN C2
         ;
       end;
       When (Country='') do;
         EXEC SQL
           DECLARE C3 CURSOR WITH RETURN WITH HOLD FOR
             SELECT * FROM ENGLAND.CITYTABLE
               WHERE CITY=:CITY
         ;
         EXEC SQL
           OPEN C3
         ;
       end;
       Otherwise do;
         EXEC SQL
           DECLARE C4 CURSOR WITH RETURN WITH HOLD FOR
             SELECT * FROM ENGLAND.CITYTABLE
               WHERE CITY=:CITY AND COUNTRY=:COUNTRY
         ;
         EXEC SQL
           OPEN C4
         ;
       end;
     end;
     if Sqlcode=0 then Response='Select successful';
     else do;
       Sqlcode_Pic = Sqlcode;
       Response='Select unsuccessful: Sqlcode '||Sqlcode_Pic;
     end;
   end;
   when ('I','INSERT') do;
     Select;
       When (City='' & Country='')
         Response='Insert unsuccessful: City and Country not specified';
       When (City='')
         Response='Insert unsuccessful: City not specified';
       When (Country='')
         Response='Insert unsuccessful: Country not specified';
       Otherwise do;
         EXEC SQL
           INSERT INTO ENGLAND.CITYTABLE
             (CITY, COUNTRY) VALUES (:CITY, :COUNTRY)
         ;
         if Sqlcode=0 then Response='Insert successful';
         else do;
           Response='Insert unsuccessful: Sqlcode '||Sqlcode_Pic;
         end;
       end;
     end;
   end;
   when ('DL','DELETE') do;
     Select;
       When (City='' & Country='')
         Response='Delete unsuccessful: City and Country not specified';
       When (City='')
         Response='Delete unsuccessful: City not specified';
       When (Country='')
         Response='Delete unsuccessful: County not specified';
       Otherwise do;
         EXEC SQL
           DELETE FROM ENGLAND.CITYTABLE
             WHERE CITY=:CITY AND COUNTRY=:COUNTRY
         ;
         if Sqlcode >= 0 then Response='Delete successful';
         else do;
           Sqlcode_Pic = Sqlcode;
           Response='Delete unsuccessful: Sqlcode '||Sqlcode_Pic;
         end;
       end;
     end;
   end;
   otherwise Response='Action "'||Action||'" not supported';
 end;

 END LEESAMP1;