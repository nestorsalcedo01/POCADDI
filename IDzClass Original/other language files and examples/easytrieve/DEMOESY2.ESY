** ***** ***** ***** ***** ***** ***** ***** ***** ***** ***** ***** **
* THIS EASYTRIEVE PRODUCES A EXTRACT FILE OF LMS COMMERCIAL LOANS     *
*      FOR THE PROFITABILITY MANAGEMENT SYSTEM.                       *
*                                                                     *
* THE INPUTS.                                                         *
*       1)THE LMS CL PROOF FILE.                                      *
*       2)THE LMS CL CONTROL FILE.                                    *
*       3)THE LMS CL OBLIGATIONS MASTER.                              *
*       4)THE LMS CL INVOICE FILE.                                    *
*       5)THE CL ACH TRANSACTION FILE.                                *
*                                                                     *
* THE OUTPUT.                                                         *
*       1)EXTRACT FILE TO CREATE TRANSACTION WAREHOUSE RECORDS        *
*                                                                     *
* THE REPORTS.                                                        *
*       1)DELINQUENCY REPORT (COMMENTED OUT).                         *
*       2)ALL OTHER REPORTS ARE USED TO CREATE SUMMARY FILES ONLY.    *
** ***** ***** ***** ***** ***** ***** ***** ***** ***** ***** ***** **
FILE M8104D  VS (300)
  C$-KEY                            2   30  A
  C$-001-DELETE-CODE                1    1  A
  C$-006-RECORD-CODE                2    3  N
  C$H001-DELETE-CODE                1    1  A
  C$H006-RECORD-CODE                2    3  N
  C$H008-BANK-NUMBER                5    2  N
  C$H009-APPLICATION-CODE           7    1  N
  C$H014-SEQUENCE-NUMBER           14    2  N
  C$H030-CURRENT-DATE              32    6  N
  C$H032-NEXT-WORK-DATE            38    6  N
  C$H036-LAST-ACCRUAL-DATE         44    6  N
  C$H050-BANK-NAME                 67   50  A
  C$W012-REPORT-NUMBER              8    8  A
  C$W051-LEFT-REPORT-DESCRIPTION   48   30  A
  C$W052-RIGHT-REPORT-DESCRIPTION  78   30  A
  C$W100-SPECIAL-REPORT-DATA      133   30  A
*
FILE M8124D  VS (2446)
  M-KEY                             2   22  A
  M-001-DELETE-CODE                 1    1  A
  M-005-BANK-NUMBER                 2    2  N
  M-010-OBLIGOR-NUMBER              4   10  N
  M-012-APPLICATION-CODE           14    1  N
  M-015-OBLIGATION-NUMBER          15    6  P 0
  M-025-RECORD-CODE                21    3  N
  M-DATE                           27    6  N
  MC001-DELETE-CODE                 1    1  A
  MC005-BANK-NUMBER                 2    2  N
  MC010-OBLIGOR-NUMBER              4   10  N
  MC012-APPLICATION-CODE           14    1  N
  MC015-OBLIGATION-NUMBER          15    6  P 0
  MC025-RECORD-CODE                21    3  N
  MC038-PROCESS-TYPE               24    4  N
  MC040-OBLIGATION-SHORT-NAME      29   15  A
  MC043-OBLIGATION-TYPE            50    2  P
  MC044-EFFECTIVE-DATE             52    4  P 0  MASK 'ZZ9/99/99'
  MC045-POSTED-DATE                56    4  P 0  MASK 'ZZ9/99/99'
  MC048-LEGAL-MATURITY-DATE        68    4  P 0  MASK 'ZZ9/99/99'
  MC050-DATE-OUT-OF-DEBT           76    4  P 0  MASK 'ZZ9/99/99'
  MC051-LAST-DAILY-TRAN-JOURNAL-DT 80    4  P 0  MASK 'ZZ9/99/99'
  MC060-ORIGINAL-PRINCIPAL-BALANCE 88    8  P 2
  MC061-CURRENT-PRINCIPAL-BALANCE  96    8  P 2
  MC062-MONTHLY-CUMMULATIVE-BAL   104    8  P 2
  MC066-CHARGE-OFF-BALANCE        136    8  P 2
  MC070-ORIGINAL-OBLIGATION-AMOUN 152    8  P 2
  MC071-ORIGINAL-OBLIGATION-DATE  160    4  P 0
  MC080-SERVICE-UNIT              191    5  N
  MC081-ASSIGNMENT-UNIT           196    5  N
* MC082-CONTACT-OFFICER           201    5  N
  MC082-CONTACT-OFFICER           201    5  A
  MC083-GENERAL-LEDGER-CODE       206    7  N
  MC091-EXAMINER-CODE-1           222    2  N
  FC151-AMOUNT-TAKENDOWN          281    8  P 2
  MC111-DURATION-CODE             250    1  N
  MC114-DISCOUNT-CODE             256    1  N
  MC143-SECURED-CODE              267    1  N
  MC151-PRINCIPAL-BILLED-NOT-PAID 281    8  P 2
  MC354-USER-AMOUNT               609    8  P 2
  MC400-ZERO-BALANCE-PROCESS-DATE 680    4  P 0
  MC410-DIVISION-NUMBER           684    3  N
  MC411-REGION-NUMBER             687    3  N
  FC438-ADVISED-COMMITTMENT-SWITCH 764   1  N
  FC475-AVAILABILITY              765    8  P 2
  MC544-CUMMULATIVE-CHGOFF-BAL-MTD 877   8  P 2
  MC546-INTEREST-PAID-TO-PRINCIPAL 885   8  P 2
  MC700-PARTICIPATION-REF-OBLIGR 1051    6  P 0
  MC701-PARTICIPATION-SOLD-APPL  1057    1  N
  MC702-PARTICIPATION-SOLD-OBLGT 1058    6  P 0
  MC705-PARTICIPATION-SOLD-TYPE  1073    1  A
  MC730-PARTICIPATION-SEGMENT-RE 1166    2  P
* MC732-TOTAL-PARTICIPATED-AMOUN 1077    8  P 2
  MC732-TOTAL-PARTICIPATED-AMOUN 1102    8  P 2
  MC900-NUMBER-OF-SEGMENTS       1835    2  P 0
  MC950-SEGMENT-FORMAT-CODE      1837    2  P 0
* MC950-PRIMARY-SEGMENT          1237  598  A
  MC950-PRIMARY-SEGMENT          1837  598  A
  NS001-DELETE-CODE                 1    1  A
  NS005-BANK-NUMBER                 2    2  N
  NS010-OBLIGOR-NUMBER              4   10  N
  NS012-APPLICATION-CODE           14    1  N
  NS015-OBLIGATION-NUMBER          15    6  P 0
  NS025-RECORD-CODE                21    3  N
  NS040-ACTIVE-NUMBER-OF-SEGMENTS  24    1  P 0
  NS100-SEGMENT-FORMAT-CODE        25    2  P 0
  NS110-NUMBER-OF-ACTIVE-ENTRIES   27    2  P 0
*     NS-SEGMENT                   25  404  A    OCCURS (4) -
      NS-SEGMENT                   25  604  A    OCCURS (4) -
         INDEX (SEG-NDX)
*    CURRENT-SEGMENT     NS-SEGMENT    404  A
     CURRENT-SEGMENT     NS-SEGMENT    604  A

FILE M8134D VS (150 )
  V-KEY                             2   27  A
  V-001-DELETE-CODE                 1    1  A
  V-005-BANK-NUMBER                 2    2  N
  V-010-OBLIGOR-NUMBER              4   10  N
  V-013-INVOICE-NUMBER             14   10  N
  V-025-RECORD-CODE                24    5  N
  VI001-DELETE-CODE                 1    1  A
  VI005-BANK-NUMBER                 2    2  N
  VI010-OBLIGOR-NUMBER              4   10  N
  VI013-INVOICE-NUMBER             14   10  N
  VI025-RECORD-CODE                24    5  N
  VI055-OBLIGATION-NUMBER          35    6  P 0
  VI060-TOTAL-AMOUNT-BILLED-THIS-P 41    8  P 2  +
        HEADING 'INVOICE TOTAL'     MASK BWZ
  VI070-INVOICE-BILLING-DATE       49    4  P 0
  VI072-INVOICE-BILLING-DUE-DATE   53    4  P 0
  VI074-FUTURE-OR-CURRENT-CODE     57    1  A
  VI-INVOICE-DATA                  88   57  A
      VI-SEGMENT                   88   19  A    OCCURS (3) -
         INDEX (VI-SEG-NDX)
     CURRENT-VI-SEGMENT  VI-SEGMENT     19  A
  VI200-CHARGE-CODE                  VI-SEGMENT               3  N
  VI210-INVOICE-AMOUNT-BILLED-THIS-C VI-SEGMENT +3            8  P 2
  VI220-INVC-AMOUNT-THIS-PERIOD      VI-SEGMENT +11           8  P 2

FILE M8234D  VS (450)
  PROOF-FILE-KEY                    2   22  A
  PROOF-TRAN-RECORD               107   80  A

FILE ACH
  ACH-RECORD                        1   80  A
*
FILE ME7231
  EXT-CRITERIA                      1     5 N
  EXT-BANK                          6     2 N
  EXT-BRANCH                        8     5 N
  EXT-TYPE                         13     5 A
  EXT-DATE                         18     6 N
  EXT-COUNT                        24    10 P 0
  EXT-AMOUNT                       24    10 P 2

FILE ME7232
  EXT-CRITERIA2                     1     5 N
  EXT-BANK2                         6     2 N
  EXT-BRANCH2                       8     5 N
  EXT-OBLIGOR2                     13    10 N
  EXT-OBLIGAT2                     23    10 N
  EXT-TYPE2                        33     5 A
  EXT-DATE2                        38     6 N
  EXT-AMOUNT2                      44    10 P 2
  EXT-MAT-DT2                      54     6 N
  EXT-ORIG-DT2                     60     6 N

FILE SMYFIL VIRTUAL F 43
  SMY-CRITERIA                      1     5 N
  SMY-BANK                          6     2 N
  SMY-BRANCH                        8     5 N
  SMY-TYPE                         13     5 A
  SMY-DATE                         18     6 N
  ACCOUNTS-PER-TYPE                24    10 P 0
  SMY-AMOUNT                       34    10 P 2

FILE SMYFIL2 VIRTUAL F 43
  SMY2-CRITERIA                     1     5 N
  SMY2-BANK                         6     2 N
  SMY2-BRANCH                       8     5 N
  SMY2-TYPE                        13     5 A
  SMY2-DATE                        18     6 N
  ACCOUNTS-PER-TYPE2               24    10 P 0
  SMY2-AMOUNT                      34    10 P 2

*
*   WORK AREA
*
*
   NS-OCCRS                W     3  N
   RK-OCCRS                W     3  N
   VI-OCCRS                W     3  N
   TVI-OCCRS               W     3  N
   ITR-OCCRS               W     3  N

*
* PE-INTEREST-HEADER    W                                    404  A
  PE-INTEREST-HEADER    W                                    604  A
  PE002-INTEREST-SEGMENT-FORMAT-C    PE-INTEREST-HEADER        2  P 0
  PE010-INTEREST-HEADER-CHARGE-CD    PE-INTEREST-HEADER +4     3  N
 *PE020-INTEREST-HEADER-EFF-FROM-DT  PE-INTEREST-HEADER +7     4  P 0
 *PE030-INTEREST-HEADER-EFF-TO-DT    PE-INTEREST-HEADER +11    4  P 0
 *PE040-INTEREST-HEADER-RATE         PE-INTEREST-HEADER +15    6  P 8
  PE045-INTEREST-MEMO-ITEM           PE-INTEREST-HEADER +11    1  N
 *PE510-INTEREST-PRIME-NUMBER        PE-INTEREST-HEADER +135   2  P 0
 *PE521-INTEREST-PRIME-FACTOR-ONE    PE-INTEREST-HEADER +139   1  N
 *PE540-INTEREST-PRIME-FLOOR         PE-INTEREST-HEADER +149   4  P 5
 *PE549-INTEREST-PRIME-CEILING       PE-INTEREST-HEADER +153   4  P 5
  PE600-INTEREST-EARNED-ITD          PE-INTEREST-HEADER +164   8  P 2
  PE604-INTEREST-EARNED-MTD          PE-INTEREST-HEADER +180   8  P 2
  PE606-INTEREST-EARNED-BTD          PE-INTEREST-HEADER +188   8  P 2
  PE630-INTEREST-PAID-ITD            PE-INTEREST-HEADER +236   8  P 2
  PE730-INTEREST-BILLED-NOT-RECVD    PE-INTEREST-HEADER +372   8  P 2

*                                                                       00002220
* RK-PRIME-SCHEDULES    W                                    404  A     00002230
  RK-PRIME-SCHEDULES    W                                    604  A     00002230
  RK002-PRIM-SCH-SEGMENT-FORMAT-C    RK-PRIME-SCHEDULES        2  P 0   00002240
  RK004-PRIM-SCH-ACTIVE-ENTRIES      RK-PRIME-SCHEDULES +2     2  P 0   00002240
*     RK-BUCKET                      RK-PRIME-SCHEDULES +4    40  A  -  00002410
      RK-BUCKET                      RK-PRIME-SCHEDULES +4    60  A  -  00002410
                                         OCCURS (10) INDEX (RKBKT-NDX)  00002420
*     CURRENT-PRIME-SCH              RK-BUCKET                40  A     00002430
      CURRENT-PRIME-SCH              RK-BUCKET                60  A     00002430
  RK010-PRIM-SCH-CHARGE-CODE         RK-BUCKET                 3  N     00002250
  RK020-PRIM-SCH-EFFECTIVE-FROM-DATE RK-BUCKET          +3     4  P 0   00002260
  RK030-PRIM-SCH-EFFECTIVE-TO-DATE   RK-BUCKET          +7     4  P 0   00002270
  RK040-PRIM-SCH-PRIME-NUMBER        RK-BUCKET          +11    3  P 0   00002280
                                                                        00002370
* SEGMENT-RECORD        W                                    404  A
  SEGMENT-RECORD        W                                    604  A
  SEGMENT-FORMAT-CODE                SEGMENT-RECORD            2  P 0
  ACTIVE-ENTRIES                     SEGMENT-RECORD     +2     2  P 0

  KEY-C$-KEY            W                   30  A VALUE ' '
  KEY-C$-006-RECORD-CODE      KEY-C$-KEY     3  N
  KEY-C$H008-BANK-NUMBER      KEY-C$-KEY +3  2  N
  KEY-C$H009-APPLICATION-CODE KEY-C$-KEY +5  1  N
  KEY-C$W012-REPORT-NUMBER    KEY-C$-KEY +6  8  A
  KEY-C$H014-SEQUENCE-NUMBER  KEY-C$-KEY +12 2  N
*
  KEY-M-KEY             W                   22  A VALUE ' '
  KEY-M-005-BANK-NUMBER       KEY-M-KEY      2  N
  KEY-M-010-OBLIGOR-NUMBER    KEY-M-KEY  +2 10  N
  KEY-M-012-APPLICATION-CODE  KEY-M-KEY +12  1  N
  KEY-M-015-OBLIGATION-NUMBER KEY-M-KEY +13  6  P 0
  KEY-M-025-RECORD-CODE       KEY-M-KEY +19  3  N
  KEY-M-025-RECORD-CODE-1     KEY-M-KEY +19  1  N

  KEY-V-KEY             W                   27  A VALUE ' '
  KEY-V-005-BANK-NUMBER       KEY-V-KEY      2  N
  KEY-V-010-OBLIGOR-NUMBER    KEY-V-KEY  +2 10  N
  KEY-V-013-INVOICE-NUMBER    KEY-V-KEY +12 10  N
  KEY-V-025-RECORD-CODE       KEY-V-KEY +22  5  N

  KEY-V-INVOICE-NUMBER  W                   10  A VALUE ' '
     KEY-V-YEAR             KEY-V-INVOICE-NUMBER    2 N
     KEY-V-APPLICATION-CODE KEY-V-INVOICE-NUMBER +2 1 N
     KEY-V-SEQUENCE-NUMBER  KEY-V-INVOICE-NUMBER +3 7 N

  TRAN-WORK             W                  80  A                        00401000
   T-BANK                   TRAN-WORK       2  N                        00400000
   T-APPLICATION            TRAN-WORK +2    1  N VALUE 1                00420000
   T-BATCH                  TRAN-WORK +3    3  A                        00420000
   T-SEQ                    TRAN-WORK +6    4  N                        00430000
   T-OBLIGOR                TRAN-WORK +10  10  N                        00420000
   T-OBLIGATION             TRAN-WORK +20  10  N                        00420000
   T-ITEM                   TRAN-WORK +25   5  A                        00420000
   T-TRAN-CODE              TRAN-WORK +30   4  N                        00430000
   T-RECORD-CODE            TRAN-WORK +34   3  A                        00400000
   T-DBID                   TRAN-WORK +37   5  A                        00420000
   T-DBID-1                 TRAN-WORK +37   1  A                        00420000
   T-SEGMENT                TRAN-WORK +42   1  N                        00420000
   T-BUCKET                 TRAN-WORK +43   2  N                        00430000
   T-SIGN                   TRAN-WORK +45   1  A                        00420000
   T-VALUE                  TRAN-WORK +46  26  A                        00420000
   T-INVOICE                TRAN-WORK +60  10  N                        00420000
   T-SECURITY               TRAN-WORK +72   2  N                        00430000
   T-AUTH-DBID              TRAN-WORK +74   1  N                        00430000
   T-AUTH-VALUE             TRAN-WORK +75   5  N                        00430000
                                                                        05253000
  START-OBLIGAT-NUMBER  W                    6  P 0 VALUE 0
  UNPACK-OBLIGAT-NUMBER W                   10  N   VALUE 0
  PACK-OBLIGAT-NUMBER   W                    6  P 0 VALUE 0

  HOLD-RECORD-CODE      W                 3  N
  HOLD-DISCOUNT-CODE    W                 1  N
  HOLD-M-KEY            W                22  A VALUE ' '
  HOLD-INTEREST-RATE    W                 4  N 2
  HOLD-NS-OCCRS         W                 3  N

  HOLD-PROCESS-TYPE     W              4  N   HEADING ('PROCESS' 'TYPE')
   PROCESS-TYPE-LEVEL   HOLD-PROCESS-TYPE 2  N
   PROCESS-TYPE-LOW     HOLD-PROCESS-TYPE +2 2 N
   PROCESS-TYPE-3       HOLD-PROCESS-TYPE +2 1 N

LNE-HEADER           W       132 A
  LNE-BANK-LIT          LNE-HEADER        7 A VALUE 'BANK'
  LNE-BANK-NUMBER       LNE-HEADER +7     2 N
  LNE-SPACE             LNE-HEADER +9     3 A VALUE ' '
  LNE-DATE              LNE-HEADER +12   11 A
  LNE-REPORT-LIT        LNE-HEADER +23    12 A VALUE 'REPORT ID - '
  LNE-REPORT-NUMBER     LNE-HEADER +35     9 A VALUE 'LNE999   '
  LNE-REPORT-NAME       LNE-HEADER +44    75 A VALUE    +
                                      '             WYZ FINANCIAL CORP'
  LNE-PAGE-LIT          LNE-HEADER +119  13 A VALUE 'PAGE'

COMMITMENT-AMOUNT W  15  N 2
PRINCIPAL-BALANCE W  15  N 2
INTEREST-OWING    W  13  N 2
FEES-OWING        W  13  N 2
OBLIGAT-HST-TOT   W  13  N 2
W-ORIGINAL        W  15  N 2
W-PRINCIPAL       W  15  N 2
W-INTEREST        W  15  N 2
W-INTEREST-FEE    W  15  N 2
W-SHORT-NAME W  15  A
W-BANK       W   2  N  VALUE 00    HEADING 'BANK'
W-OBLIGOR    W  10  N            MASK '999-999-999-9'
W-OBLIGAT    W  10  N            MASK '99999-9999-9'
W-SERVICE    W   5  N
W-ASSIGNMENT W   5  N      HEADING ('ASSIGNMENT UNIT')
*W-CONTACT    W   5  N
W-CONTACT    W   5  A
W-REGION     W   3  N
W-DIVISION   W   3  N
W-INTEREST-RATE W 4 N 2
W-SECURED-CODE W 1  N      HEADING ('SEC' 'CD')
W-BANK-NAME  W  50  A   VALUE ' '
W-REPORT-DATA  W 300 A
 DELQ-CATEGORY1 W-REPORT-DATA    3 N
 DELQ-CATEGORY2 W-REPORT-DATA +3 3 N
 DELQ-CATEGORY3 W-REPORT-DATA +6 3 N
 DELQ-CATEGORY4 W-REPORT-DATA +9 3 N
W-ACCRUAL-DATE W 6  N
 ACCRUAL-YEAR  W-ACCRUAL-DATE    2 N
 ACCRUAL-MONTH W-ACCRUAL-DATE +2 2 N
 DAYS-IN-MONTH W-ACCRUAL-DATE +4 2 N
W-LAST-DAILY-TRAN W 6 N
 W-LAST-TRAN-YY W-LAST-DAILY-TRAN    2 N
 W-LAST-TRAN-DD W-LAST-DAILY-TRAN +2 2 N
W-ZERO-BAL-PROC-D W 6 N
 W-ZERO-BAL-YY  W-ZERO-BAL-PROC-D    2 N
 W-ZERO-BAL-MM  W-ZERO-BAL-PROC-D +2 2 N
 W-ZERO-BAL-DD  W-ZERO-BAL-PROC-D +4 2 N
W-TIE-TYPE     W 9  A   VALUE ' ' HEADING ('REFERENCE' 'TYPE')
W-STATUS-CODE W  1  A   VALUE ' '
W-AMOUNT      W 15  N 2 VALUE 0
W-LOAN-LOSS       W  15  N 2
W-GL-CODE    W   7  N      HEADING ('GENERAL' 'LEGER' 'CODE')
W-GL-ACCOUNT W   5  N      HEADING ('GENERAL' 'LEGER' 'ACCOUNT')
CRITERIA       W  5 N   VALUE 00000
REFERENCE-TYPE W 37 A   VALUE ' ' HEADING ('REFERENCE' 'DESCRIPTION')
PRIMARY-REFERENCE-OBLIGOR   W 10 N VALUE 0 MASK (BWZ '999-999-999-9') +
                       HEADING ('PRIMARY' 'REFERENCE' 'OBLIGOR')
REFERENCE-OBLIGOR   W 10 N VALUE 0 MASK (BWZ '999-999-999-9') +
                       HEADING ('INDIRECT' 'OBLIGOR')
REFERENCE-OBLIGAT   W 10 N VALUE 0 MASK (BWZ '99999-9999-9') +
                       HEADING ('INDIRECT' 'OBLIGAT/ITEM')
REFERENCE-ITEM      W  5 N VALUE 0 MASK (BWZ '9999-9') +
                       HEADING ('ITEM')
W-SW         W   1  N   VALUE 0
PROC-DATE    W   6  N   VALUE 0
ACCR-DATE    W   6  N   VALUE 0
ACCRUAL-DATE W   6  N   VALUE 0 MASK 'Z9/99/99'
W-MAT-DATE   W   6  N   VALUE 0
W-ORIG-DATE  W   6  N   VALUE 0
EXTRACT-SW   W   1  A   VALUE 'N'
DELQ-LEVEL   W   1  N   VALUE 0
DELINQ-SW    W   1  A   VALUE ' '
TABLE-SW     W   1  A   VALUE ' '
NRF-OBLIGATION W 1  A   VALUE ' '
ZERO-SW      W   1  A   VALUE 'Y'
CURRENT-MAX-ITR-OCCRS    W  3 N VALUE 0
  W-COUNT                    W            8  N 0 VALUE 0
 CHARGE-CODE       W     3  N    VALUE 0
 BILLED-AMOUNT     W    15  N 2  VALUE 0
 PAST-DUE-AMOUNT   W    15  N 2  VALUE 0
 REPORT-PAST-DUE   W    15  N 2  VALUE 0
 TEST-AMOUNT       W    15  N 2  VALUE 0
 LEAPS             W     2  N 0  VALUE 0
 DIFF              W     3  N 0  VALUE 0
 START-JULIAN      W     5  N 0
  START-YY  START-JULIAN    2 N
  START-JJJ START-JULIAN +2 3 N
 END-JULIAN        W     5  N 0
  END-YY    END-JULIAN    2 N
  END-JJJ   END-JULIAN +2 3 N

INVOICE-TABLE                    W                    6500  A
  INVOICE-TABLE-REC               INVOICE-TABLE         65  A     -
         OCCURS (100)   INDEX (ITR-SEG-NDX)
  TVI070-INVOICE-BILLING-DATE     INVOICE-TABLE-REC      4  P 0
  TVI072-INVOICE-BILLING-DUE-DATE INVOICE-TABLE-REC +4   4  P 0
  TVI-INVOICE-DATA                INVOICE-TABLE-REC +8  57  A
      TVI-SEGMENT                 INVOICE-TABLE-REC +8  19  A     -
         OCCURS (3)     INDEX (TVI-SEG-NDX)
     CURRENT-TVI-SEGMENT  TVI-SEGMENT     19  A
  TVI200-CHARGE-CODE                  TVI-SEGMENT             3  N
  TVI210-INVOICE-AMOUNT-BILLED-THIS-C TVI-SEGMENT +3          8  P 2
  TVI220-INVC-AMOUNT-THIS-PERIOD      TVI-SEGMENT +11         8  P 2

DATE-IN      W      6 N
 YY-IN  DATE-IN     2 N
 MM-IN  DATE-IN +2  2 N
 DD-IN  DATE-IN +4  2 N

DATE-INA     W      6 N
 YY-INA  DATE-INA     2 N
 MM-INA  DATE-INA +2  2 N
 DD-INA  DATE-INA +4  2 N

DATE-INB     W      6 N
 YY-INB  DATE-INB     2 N
 MM-INB  DATE-INB +2  2 N
 DD-INB  DATE-INB +4  2 N

DATE-INC     W      6 N
 YY-INC  DATE-INC     2 N
 MM-INC  DATE-INC +2  2 N
 DD-INC  DATE-INC +4  2 N

WORK-DATEA   W      6 N

WORK-DATEB   W      6 N

DATE-IN2     W      6 N
 YY-IN2 DATE-IN2    2 N
 MM-IN2 DATE-IN2 +2 2 N
 DD-IN2 DATE-IN2 +4 2 N

DATE-OUT     W      6 N MASK 'Z9/99/99'
 MM-OUT DATE-OUT    2 N
 DD-OUT DATE-OUT +2 2 N
 YY-OUT DATE-OUT +4 2 N

DATE-RPT     W      8 A
 MM-RPT DATE-RPT    2 N
 S1-RPT DATE-RPT +2 1 A VALUE '/'
 DD-RPT DATE-RPT +3 2 N
 S2-RPT DATE-RPT +5 1 A VALUE '/'
 YY-RPT DATE-RPT +6 2 N

SYS-DATE-IN  W              8 A
 MM-SYS-IN  SYS-DATE-IN     2 N
 MM-SYS-IN-A SYS-DATE-IN    1 A
 MM-SYS-IN-N SYS-DATE-IN    1 N
 S1-SYS-IN  SYS-DATE-IN  +2 1 A VALUE '/'
 DD-SYS-IN  SYS-DATE-IN  +3 2 N
 S2-SYS-IN  SYS-DATE-IN  +5 1 A VALUE '/'
 YY-SYS-IN  SYS-DATE-IN  +6 2 N

BUS-DATE     W              6 A
 BUS-CC     BUS-DATE        2 N
 BUS-YY     BUS-DATE     +2 2 N
 BUS-MM     BUS-DATE     +4 2 N
*
  WORK-TYPE       W          5  A         HEADING ('TYPE' 'CODE')
   RATE-CODE         WORK-TYPE       1 A
   OBLIGATION-TYPE   WORK-TYPE +1    3 N
   WORK-FILLER       WORK-TYPE +4    1 A
*
  DESCRIPTION     W          5  A
   REAL-GL-CODE      DESCRIPTION     5 N  HEADING ('REAL' 'GL CODE')
*
FILE GLCODES  TABLE INSTREAM
ARG    1  7  N
DESC   9  5  A
0011200 11200
0016110 16110
0017661 17661
0028237 28237
0110005 11821
0110006 11821
0110203 11821
0110303 11821
0110403 11821
0110404 11821
0110503 11821
0110603 11821
0110703 11821
0110704 11821
0110802 11821
0110803 11821
0112204 11821
0112205 11821
0112207 11821
0112303 11821
0112332 11821
0112402 11821
0112403 11821
0112501 11821
0112601 11821
0210005 12821
0210006 12821
0210203 12821
0210303 12821
0210403 12821
0210503 12821
0210603 12821
0210703 12821
0210704 12821
0210802 12821
0210803 12821
0212204 12821
0212205 12821
0212304 12821
0212332 12821
0212364 12821
0212402 12821
0212403 12821
0212501 12821
0212502 12821
0212601 12821
0212703 12821
0310005 12831
0310006 12831
0310203 12831
0310303 12831
0310403 12831
0310404 12831
0310503 12831
0310603 12831
0310703 12831
0310704 12831
0310802 12831
0310803 12831
0312204 12831
0312205 12831
0312332 12831
0312364 12831
0312402 12831
0312403 12831
0312501 12831
0312601 12831
0410005 11091
0410006 11093
0410203 11095
0410303 11097
0410403 11091
0410404 11093
0410503 11095
0410603 11097
0410734 11091
0410735 11093
0410736 11095
0410737 11097
0410739 20261
0410803 11091
0410835 11093
0410836 11095
0410837 11097
0412204 11341
0412205 11341
0412239 20261
0412304 11341
0412332 11341
0412338 20260
0412364 11341
0412402 11341
0412403 11341
0412501 11341
0412601 11341
0412703 20260
ENDTABLE
*                                       *
*             LINKAGE FIELDS            *
*                                       *

JULIAN            W        5 N VALUE 0
GREGORIAN         W        6 N VALUE 0 MASK '99-99-99'
REQUEST-SW        W        1 N VALUE 0
ERROR-SW          W        1 N VALUE 0

*
JOB INPUT M8234D START(GET-PROCESS-DATE) FINISH(ACH-PROC)
  TRAN-WORK = PROOF-TRAN-RECORD
  PERFORM TEST-TRANS

 TEST-TRANS. PROC
  IF T-BANK NOT NUMERIC OR T-OBLIGOR NOT NUMERIC OR T-SEQ NOT NUMERIC
     GO TO END-TEST-TRANS
  END-IF
   IF T-BANK NE W-BANK
        MOVE SPACES             TO  KEY-C$-KEY
        MOVE 050                TO  KEY-C$-006-RECORD-CODE
        MOVE T-BANK             TO  KEY-C$H008-BANK-NUMBER
        MOVE 1                  TO  KEY-C$H009-APPLICATION-CODE
        MOVE 01                 TO  KEY-C$H014-SEQUENCE-NUMBER
        READ M8104D KEY KEY-C$-KEY, STATUS
        IF EOF M8104D, OR FILE-STATUS (M8104D) NOT ZERO
               DISPLAY ' UNSUCCESSFUL POINT FOR THIS CONTROL RECORD ' +
                        KEY-C$-KEY
               DISPLAY ' TRAN = ' TRAN-WORK
           STOP
        END-IF
        MOVE C$H030-CURRENT-DATE TO DATE-IN
        MOVE C$H030-CURRENT-DATE TO DATE-INC
        MOVE C$H008-BANK-NUMBER TO W-BANK
        MOVE C$H050-BANK-NAME  TO  W-BANK-NAME
        MOVE C$H036-LAST-ACCRUAL-DATE TO W-ACCRUAL-DATE
        MOVE W-ACCRUAL-DATE           TO  DATE-IN
        MOVE DATE-IN     TO ACCR-DATE
   END-IF
*
  IF T-TRAN-CODE EQ 4000 THRU 4009
     KEY-V-005-BANK-NUMBER     = T-BANK
     KEY-V-010-OBLIGOR-NUMBER  = T-OBLIGOR
     KEY-V-025-RECORD-CODE     = 00000
     KEY-V-013-INVOICE-NUMBER  = T-INVOICE
     POINT M8134D   GE KEY-V-KEY, STATUS
     IF EOF M8134D , OR FILE-STATUS (M8134D) NOT ZERO
            DISPLAY ' UNSUCCESSFUL POINT FOR THIS INVOICE    RECORD ' +
                    '  KEY = ' KEY-V-KEY
            DISPLAY '      T-WORK    ' TRAN-WORK
        GO TO END-TEST-TRANS
     END-IF
     GET   M8134D
     IF EOF M8134D , OR FILE-STATUS (M8134D) NOT ZERO
            DISPLAY ' UNSUCCESSFUL READ  FOR THIS INVOICE    RECORD ' +
                    '    KEY = ' KEY-V-KEY
            DISPLAY '      T-WORK    ' TRAN-WORK
        GO TO END-TEST-TRANS
     END-IF
     IF T-INVOICE NOT NUMERIC
            DISPLAY 'INVOICE NUMBER NOT NUMERIC ' TRAN-WORK
        GO TO END-TEST-TRANS
     END-IF
     IF VI013-INVOICE-NUMBER NE T-INVOICE
            DISPLAY '  INVOICE NOT FOUND      '   TRAN-WORK
        GO TO END-TEST-TRANS
     END-IF
     T-OBLIGATION = VI055-OBLIGATION-NUMBER
     PERFORM OBLIGATION-READ
     IF NRF-OBLIGATION = 'Y'
        GO TO END-TEST-TRANS
     END-IF
     CRITERIA = 00502
     PRINT TRAN-RPT
    *READ OBLIGATION FOR ASSIGNMENT UNIT - WRITE REPORT
     GO TO END-TEST-TRANS
  END-IF
  IF T-TRAN-CODE EQ 4022, 4042
     PERFORM OBLIGATION-READ
     IF NRF-OBLIGATION = 'Y'
        GO TO END-TEST-TRANS
     END-IF
     CRITERIA = 00502
     PRINT TRAN-RPT
    *READ OBLIGATION FOR ASSIGNMENT UNIT - WRITE REPORT
     GO TO END-TEST-TRANS
  END-IF
  IF T-TRAN-CODE EQ 3402, 3502, 3602
     PERFORM OBLIGATION-READ
     IF NRF-OBLIGATION = 'Y'
        GO TO END-TEST-TRANS
     END-IF
     CRITERIA = 00501
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
 ****PRINT TRAN-RPT
    *READ OBLIGATION FOR ASSIGNMENT UNIT - WRITE REPORT
     GO TO END-TEST-TRANS
  END-IF
  IF T-TRAN-CODE EQ 3902
     PERFORM OBLIGATION-READ
     IF NRF-OBLIGATION = 'Y'
        GO TO END-TEST-TRANS
     END-IF
     IF PROCESS-TYPE-LOW NE 55
        CRITERIA = 00531
       ELSE
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
 ****** CRITERIA = 00501
 ****** ADD THE FOLLOWING
        GO TO END-TEST-TRANS
     END-IF
     PRINT TRAN-RPT
    *READ OBLIGATION FOR ASSIGNMENT UNIT - CHECK FOR TD - WRITE REPORT
     GO TO END-TEST-TRANS
  END-IF
  IF T-TRAN-CODE EQ 9881
     PERFORM OBLIGATION-READ
     IF NRF-OBLIGATION = 'Y'
        GO TO END-TEST-TRANS
     END-IF
     CRITERIA = 00002
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
 ****PRINT TRAN-RPT
     GO TO END-TEST-TRANS
  END-IF
 END-TEST-TRANS
 END-PROC

 OBLIGATION-READ. PROC
   NRF-OBLIGATION = ' '
   IF T-OBLIGOR NOT NUMERIC
         DISPLAY ' T-OBLIGOR NOT NUMERIC  ' TRAN-WORK
     NRF-OBLIGATION = 'Y'
     GO TO END-OBN-READ
   END-IF
          MOVE T-BANK                 TO  KEY-M-005-BANK-NUMBER
          MOVE T-OBLIGOR              TO  KEY-M-010-OBLIGOR-NUMBER
          MOVE T-APPLICATION          TO  KEY-M-012-APPLICATION-CODE
          KEY-M-015-OBLIGATION-NUMBER  =  T-OBLIGATION
          MOVE 300                    TO  KEY-M-025-RECORD-CODE
  IF T-TRAN-CODE EQ 9881
     MOVE 200                         TO  KEY-M-025-RECORD-CODE
  END-IF
  IF T-TRAN-CODE EQ 4000 THRU 4009
     MOVE VI074-FUTURE-OR-CURRENT-CODE TO KEY-M-025-RECORD-CODE-1
  END-IF
   READ M8124D KEY KEY-M-KEY, STATUS
   IF EOF M8124D, OR FILE-STATUS (M8124D) NOT ZERO
         DISPLAY ' END OF FILE OBLIGATION        OBLIGOR ' T-OBLIGOR
         DISPLAY '    KEY = ' KEY-M-KEY                            +
                           '  STATUS = ' FILE-STATUS (M8124D)
         DISPLAY           '               TRAN = ' TRAN-WORK
     NRF-OBLIGATION = 'Y'
     GO TO END-OBN-READ
   END-IF
     MOVE MC025-RECORD-CODE TO HOLD-RECORD-CODE
     HOLD-PROCESS-TYPE =  MC038-PROCESS-TYPE
     W-BANK     = MC005-BANK-NUMBER
     W-OBLIGOR  = MC010-OBLIGOR-NUMBER
     UNPACK-OBLIGAT-NUMBER          = MC015-OBLIGATION-NUMBER
     MOVE UNPACK-OBLIGAT-NUMBER     TO W-OBLIGAT
     W-SHORT-NAME = MC040-OBLIGATION-SHORT-NAME
     W-SERVICE    = MC080-SERVICE-UNIT
     W-ASSIGNMENT = MC081-ASSIGNMENT-UNIT
     W-GL-CODE    = MC083-GENERAL-LEDGER-CODE
     IF HOLD-RECORD-CODE EQ 200
        DESCRIPTION = 'CML99'
       ELSE
        SEARCH GLCODES WITH W-GL-CODE GIVING DESCRIPTION
        IF NOT GLCODES
           REAL-GL-CODE = 00000
           DISPLAY 'NO MATCH ' W-BANK ' ' W-OBLIGOR ' ' W-OBLIGAT +
            '   RECORD CODE ' HOLD-RECORD-CODE '  GL CODE ' W-GL-CODE
        END-IF
     END-IF
     RATE-CODE  = 'F'
     OBLIGATION-TYPE                 =  MC043-OBLIGATION-TYPE
     WORK-FILLER     = ' '
   END-OBN-READ
 END-PROC
*
 ACH-PROC. PROC
  ACH-LOOP
  GET ACH
   IF EOF ACH
     GO TO END-ACH
   END-IF
  TRAN-WORK = ACH-RECORD
  PERFORM TEST-TRANS
  GO TO ACH-LOOP
  END-ACH
 END-PROC

 GET-PROCESS-DATE. PROC
      MOVE ' ' TO KEY-M-KEY
      POINT M8124D   GE KEY-M-KEY, STATUS
      IF EOF M8124D , OR FILE-STATUS (M8124D) NOT ZERO
             DISPLAY ' UNSUCCESSFUL START FOR OBLIGATION FILE ' +
                        KEY-M-KEY
         STOP
      END-IF
      GET   M8124D
      IF EOF M8124D , OR FILE-STATUS (M8124D) NOT ZERO
         DISPLAY ' UNSUCCESSFUL READ FOR OBLIGATION INTEGRITY RECORD ' +
                   KEY-M-KEY
         STOP
      END-IF
      DATE-OUT   =     M-DATE
      MOVE MM-OUT    TO MM-IN
      MOVE DD-OUT    TO DD-IN
      MOVE YY-OUT    TO YY-IN
      MOVE DATE-IN   TO PROC-DATE
 END-PROC

  REPORT TRAN-RPT SUMMARY SUMFILE SMYFIL
   *SEQUENCE CRITERIA T-BANK W-ASSIGNMENT DESCRIPTION ACCR-DATE
   *CONTROL CRITERIA NOPRINT T-BANK NOPRINT W-ASSIGNMENT NOPRINT +
   *        DESCRIPTION NOPRINT ACCR-DATE NOPRINT
   *LINE    CRITERIA T-BANK W-ASSIGNMENT DESCRIPTION ACCR-DATE   +
    SEQUENCE CRITERIA T-BANK W-ASSIGNMENT WORK-TYPE ACCR-DATE
    CONTROL CRITERIA NOPRINT T-BANK NOPRINT W-ASSIGNMENT NOPRINT +
            WORK-TYPE NOPRINT ACCR-DATE NOPRINT
    LINE    CRITERIA T-BANK W-ASSIGNMENT WORK-TYPE ACCR-DATE   +
                                                      W-AMOUNT TALLY

*
JOB INPUT M8124D START(START-OBLIGAT-PROC) FINISH(LAST-REC-PROC)
*
 IF W-SW = 0
          PERFORM TITLE-DATE-PROC
          MOVE DATE-RPT               TO  LNE-DATE
   SYS-DATE-IN   =  SYSDATE
   IF MM-SYS-IN-A  EQ ' '
       MM-SYS-IN-N = 0
   END-IF
  W-SW = 1
  GO TO JOB
 END-IF
*
 IF EXTRACT-SW EQ 'Y'
     IF MC005-BANK-NUMBER NE W-BANK  +
              OR MC010-OBLIGOR-NUMBER NE W-OBLIGOR +
              OR MC015-OBLIGATION-NUMBER NE W-OBLIGAT
        EXTRACT-SW = 'N'
        PERFORM EXTRACT-ROUTINE
    END-IF
 END-IF
*
 IF MC001-DELETE-CODE EQ '1'
     GO TO JOB
 END-IF
*
 IF  MC005-BANK-NUMBER  NE W-BANK
      MOVE SPACES               TO  KEY-C$-KEY
      MOVE 050                  TO  KEY-C$-006-RECORD-CODE
      MOVE MC005-BANK-NUMBER    TO  KEY-C$H008-BANK-NUMBER
      MOVE 1                    TO  KEY-C$H009-APPLICATION-CODE
      MOVE 01                   TO  KEY-C$H014-SEQUENCE-NUMBER
      READ  M8104D  KEY KEY-C$-KEY, STATUS
      IF EOF M8104D,  OR FILE-STATUS (M8104D) NOT ZERO
             DISPLAY ' UNSUCCESSFUL POINT FOR THIS CONTROL RECORD ' +
                        KEY-C$-KEY
         STOP
      END-IF
      MOVE C$H030-CURRENT-DATE TO  DATE-IN
      MOVE C$H030-CURRENT-DATE TO  DATE-INC
      IF  YY-IN NE  YY-RPT  OR     +
          MM-IN NE  MM-RPT  OR     +
          DD-IN NE  DD-RPT
           DISPLAY ' MASTER FILE AND CONTROL FILE DO NOT MATCH '
           DISPLAY ' OBLIGATION FILE ' DATE-RPT
           DISPLAY ' CONTROL FILE ' C$H030-CURRENT-DATE
           STOP
      END-IF
      MOVE C$H008-BANK-NUMBER  TO  W-BANK
      MOVE C$H050-BANK-NAME    TO  W-BANK-NAME
      MOVE C$H036-LAST-ACCRUAL-DATE TO W-ACCRUAL-DATE
      MOVE W-ACCRUAL-DATE             TO  DATE-IN
      PERFORM LMS-DATE-RTN
      MOVE DATE-IN       TO ACCR-DATE
      MOVE DATE-OUT      TO ACCRUAL-DATE
      PERFORM CONVERT-DATE-PROC
      END-JULIAN = START-JULIAN
      IF YY-IN GT 1
         KEY-V-YEAR = YY-IN - 1
         KEY-V-APPLICATION-CODE = 1
         KEY-V-SEQUENCE-NUMBER = 0000000
        ELSE
         DISPLAY ' INVALID YEAR TO SUBTRACT FOR INVOICE DELINQUENCY ' +
                   DATE-IN
         STOP
      END-IF
      MOVE 150                  TO  KEY-C$-006-RECORD-CODE
      MOVE 'CL257'              TO  KEY-C$W012-REPORT-NUMBER
      READ  M8104D  KEY KEY-C$-KEY, STATUS
      IF EOF M8104D,  OR FILE-STATUS (M8104D) NOT ZERO
             DISPLAY ' UNSUCCESSFUL POINT FOR 150 CONTROL RECORD ' +
                        KEY-C$-KEY
         STOP
      END-IF
      MOVE C$W100-SPECIAL-REPORT-DATA      TO  W-REPORT-DATA
 END-IF
*
  IF MC025-RECORD-CODE EQ 200
     HOLD-PROCESS-TYPE    =  MC038-PROCESS-TYPE
    *IF (PROCESS-TYPE-LEVEL EQ 01, 02, 05, 21, 22 AND                +
    *    PROCESS-TYPE-3 NE 1)                            OR          +
    *   (PROCESS-TYPE-LEVEL EQ 31, 32 AND                            +
    *    FC438-ADVISED-COMMITTMENT-SWITCH EQ 1 AND                   +
    *    PROCESS-TYPE-3 NE 1)
    *      *** ABOVE ARE ACCEPTABLE CONDITIONS ***
    *   ELSE
    *    GO TO OBLIGATION-READ-LOOP
    *END-IF
     W-STATUS-CODE            =  'O'
     DATE-IN =  MC045-POSTED-DATE
     IF MM-IN EQ MM-RPT AND YY-IN EQ YY-RPT
        W-STATUS-CODE            =  'N'
     END-IF
     DATE-IN =  MC048-LEGAL-MATURITY-DATE
     DATE-INA =  MC048-LEGAL-MATURITY-DATE
     WORK-DATEA =  DATE-INA
     DATE-IN2 =  MC071-ORIGINAL-OBLIGATION-DATE
     WORK-DATEB =  DATE-INB
     COMMITMENT-AMOUNT = MC061-CURRENT-PRINCIPAL-BALANCE +       +
                                        FC151-AMOUNT-TAKENDOWN
     RATE-CODE  = 'F'
     OBLIGATION-TYPE                 =  MC043-OBLIGATION-TYPE
     WORK-FILLER     = ' '
     IF COMMITMENT-AMOUNT EQ 0
        IF MM-IN NE MM-RPT OR YY-IN NE YY-RPT
            GO TO JOB
        END-IF
        W-STATUS-CODE            =  'C'
     END-IF
     IF FC151-AMOUNT-TAKENDOWN EQ 0
        CRITERIA = 00164
       ELSE
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
 ****** CRITERIA = 00163
     END-IF
     W-OBLIGOR                      = MC010-OBLIGOR-NUMBER
     UNPACK-OBLIGAT-NUMBER          = MC015-OBLIGATION-NUMBER
     MOVE UNPACK-OBLIGAT-NUMBER     TO W-OBLIGAT
     MOVE MC025-RECORD-CODE TO HOLD-RECORD-CODE
     W-ASSIGNMENT = MC081-ASSIGNMENT-UNIT
     W-GL-CODE    = MC083-GENERAL-LEDGER-CODE
     W-DIVISION   = MC410-DIVISION-NUMBER
     W-REGION     = MC411-REGION-NUMBER
     W-CONTACT    = MC082-CONTACT-OFFICER
     DESCRIPTION = 'CML99'
    *SEARCH GLCODES WITH W-GL-CODE GIVING DESCRIPTION
    *IF NOT GLCODES
    *   REAL-GL-CODE = 00000
    * * DISPLAY 'NO MATCH ' W-BANK ' ' W-OBLIGOR ' ' W-OBLIGAT +
    * *  '   RECORD CODE ' HOLD-RECORD-CODE '  GL CODE ' W-GL-CODE
    *END-IF
     PRINT BAL-RPT
  END-IF
IF MC025-RECORD-CODE EQ 300
     HOLD-PROCESS-TYPE    =  MC038-PROCESS-TYPE
    *IF PROCESS-TYPE-LEVEL EQ 54, 56, 57, 59
    *    GO TO OBLIGATION-READ-LOOP
    *END-IF
    ****** DO NOT REPORT PARTICIPATIONS SOLD AS SEPERATE LOANS *****
    *IF PROCESS-TYPE-LEVEL EQ 53
    *    GO TO OBLIGATION-READ-LOOP
    *END-IF
     EXTRACT-SW = 'Y'
     ZERO-SW    = 'Y'
     DELINQ-SW  = ' '
     W-STATUS-CODE            =  'O'
     DATE-IN =  MC045-POSTED-DATE
     DATE-INA =  MC048-LEGAL-MATURITY-DATE
     WORK-DATEA =  DATE-INA
     DATE-INB =  MC071-ORIGINAL-OBLIGATION-DATE
     WORK-DATEB =  DATE-INB
     IF MM-IN EQ MM-RPT AND YY-IN EQ YY-RPT
        W-STATUS-CODE            =  'N'
     END-IF
     W-LAST-DAILY-TRAN  =  MC051-LAST-DAILY-TRAN-JOURNAL-DT
     W-ZERO-BAL-PROC-D  =  MC400-ZERO-BALANCE-PROCESS-DATE
    *CRISP-WORK-APPLICATION-CODE     =  MC043-OBLIGATION-TYPE
     RATE-CODE  = 'F'
     IF MC111-DURATION-CODE EQ 2
        RATE-CODE  = 'V'
     END-IF
     OBLIGATION-TYPE                 =  MC043-OBLIGATION-TYPE
     WORK-FILLER     = ' '
     HOLD-DISCOUNT-CODE   =  MC114-DISCOUNT-CODE
     W-ORIGINAL = MC060-ORIGINAL-PRINCIPAL-BALANCE
     IF PROCESS-TYPE-LOW EQ 51
        COMMITMENT-AMOUNT = MC060-ORIGINAL-PRINCIPAL-BALANCE
      ELSE
        COMMITMENT-AMOUNT = 0
     END-IF
     IF COMMITMENT-AMOUNT GE 0
       *CRISP-LOAN-COMMIT-AMOUNT     =  COMMITMENT-AMOUNT + .5
       ELSE
       *CRISP-LOAN-COMMIT-AMOUNT     =  COMMITMENT-AMOUNT - .5
     END-IF
     W-PRINCIPAL                     =  MC061-CURRENT-PRINCIPAL-BALANCE
     W-INTEREST-FEE                  =  0
   **PRINCIPAL-BALANCE  =                        +
   **  (MC062-MONTHLY-CUMMULATIVE-BAL     -      +
   **   MC544-CUMMULATIVE-CHGOFF-BAL-MTD) /      +
   **   DAYS-IN-MONTH                     -      +
   **   MC546-INTEREST-PAID-TO-PRINCIPAL
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
     PRINCIPAL-BALANCE  =                        +
        MC061-CURRENT-PRINCIPAL-BALANCE   -      +
        MC066-CHARGE-OFF-BALANCE          -      +
        MC732-TOTAL-PARTICIPATED-AMOUN    -      +
        MC546-INTEREST-PAID-TO-PRINCIPAL
     IF MC062-MONTHLY-CUMMULATIVE-BAL NE 0     OR +
        MC544-CUMMULATIVE-CHGOFF-BAL-MTD NE 0  OR +
        MC546-INTEREST-PAID-TO-PRINCIPAL  NE 0
        ZERO-SW = 'N'
     END-IF
     W-OBLIGOR                      = MC010-OBLIGOR-NUMBER
     UNPACK-OBLIGAT-NUMBER          = MC015-OBLIGATION-NUMBER
     MOVE UNPACK-OBLIGAT-NUMBER     TO W-OBLIGAT
     MOVE MC025-RECORD-CODE TO HOLD-RECORD-CODE
     W-ASSIGNMENT = MC081-ASSIGNMENT-UNIT
     W-GL-CODE    = MC083-GENERAL-LEDGER-CODE
     W-DIVISION   = MC410-DIVISION-NUMBER
     W-REGION     = MC411-REGION-NUMBER
     W-CONTACT    = MC082-CONTACT-OFFICER
     W-SECURED-CODE  =  MC143-SECURED-CODE
     W-LOAN-LOSS       = MC354-USER-AMOUNT
     SEARCH GLCODES WITH W-GL-CODE GIVING DESCRIPTION
     IF NOT GLCODES
        REAL-GL-CODE = 00000
       *DISPLAY 'NO MATCH ' W-BANK ' ' W-OBLIGOR ' ' W-OBLIGAT +
       * '   RECORD CODE ' HOLD-RECORD-CODE '  GL CODE ' W-GL-CODE
     END-IF
     TABLE-SW     = ' '
     IF MC151-PRINCIPAL-BILLED-NOT-PAID GT 0 AND W-PRINCIPAL NE 0
        CHARGE-CODE    = 001
        BILLED-AMOUNT  = MC151-PRINCIPAL-BILLED-NOT-PAID
        PAST-DUE-AMOUNT = MC151-PRINCIPAL-BILLED-NOT-PAID
        PERFORM TEST-DELINQUENCY
     END-IF
     W-SHORT-NAME = MC040-OBLIGATION-SHORT-NAME
     W-INTEREST-RATE   = 0
     INTEREST-OWING    = 0
     W-INTEREST-FEE    = 0
     FEES-OWING        = 0
     IF MC700-PARTICIPATION-REF-OBLIGR NE 0
        REFERENCE-OBLIGOR = MC700-PARTICIPATION-REF-OBLIGR
        REFERENCE-OBLIGAT = MC702-PARTICIPATION-SOLD-OBLGT
        MOVE MC705-PARTICIPATION-SOLD-TYPE TO W-TIE-TYPE
     END-IF
     IF MC900-NUMBER-OF-SEGMENTS EQ 0
         GO TO JOB
         END-IF
     IF MC950-SEGMENT-FORMAT-CODE EQ 100
        MOVE MC950-PRIMARY-SEGMENT TO PE-INTEREST-HEADER
        PERFORM TEST-INTEREST-HEADER
     END-IF
     IF MC950-SEGMENT-FORMAT-CODE EQ 400, 500
        MOVE MC950-PRIMARY-SEGMENT TO PE-INTEREST-HEADER
        PERFORM TEST-FEE-HEADER
     END-IF
     IF MC950-SEGMENT-FORMAT-CODE EQ 915 AND RATE-CODE EQ 'F'
        MOVE MC950-PRIMARY-SEGMENT TO RK-PRIME-SCHEDULES
      **PERFORM TEST-FEE-HEADER
        PERFORM TEST-PRIME-SCHEDULES
     END-IF
END-IF
*
  IF NS025-RECORD-CODE EQ 350 THRU 399
     IF NS010-OBLIGOR-NUMBER = W-OBLIGOR AND NS005-BANK-NUMBER EQ W-BANK
        IF NS015-OBLIGATION-NUMBER EQ W-OBLIGAT
           NS-OCCRS = 1
         SEGMENT-LOOP.
*          SEG-NDX = (NS-OCCRS - 1) * 404
           SEG-NDX = (NS-OCCRS - 1) * 604
           MOVE CURRENT-SEGMENT  TO SEGMENT-RECORD
           IF SEGMENT-FORMAT-CODE EQ 100
              MOVE SEGMENT-RECORD  TO PE-INTEREST-HEADER
              PERFORM TEST-INTEREST-HEADER
           END-IF
           IF SEGMENT-FORMAT-CODE EQ 400, 500
              MOVE SEGMENT-RECORD TO PE-INTEREST-HEADER
              PERFORM TEST-FEE-HEADER
           END-IF
           IF SEGMENT-FORMAT-CODE EQ 915 AND RATE-CODE EQ 'F'
              MOVE SEGMENT-RECORD TO RK-PRIME-SCHEDULES
            **PERFORM TEST-FEE-HEADER
              PERFORM TEST-PRIME-SCHEDULES
           END-IF
           IF NS-OCCRS LS NS040-ACTIVE-NUMBER-OF-SEGMENTS
              NS-OCCRS = NS-OCCRS + 1
              GO TO SEGMENT-LOOP
           END-IF
        END-IF
     END-IF
  END-IF
*
 POINT-OBLIGAT-PROC. PROC
      POINT M8124D   GE KEY-M-KEY, STATUS
      IF EOF M8124D , OR FILE-STATUS (M8124D) NOT ZERO
             DISPLAY ' UNSUCCESSFUL POINT FOR THIS OBLIGATION RECORD ' +
                        KEY-M-KEY
         STOP
      END-IF
 END-PROC
*
 START-OBLIGAT-PROC. PROC
      MOVE ' ' TO KEY-M-KEY
      PERFORM POINT-OBLIGAT-PROC
      W-BANK = 0
      W-OBLIGOR = 0
      W-OBLIGAT = 0
      W-SW = 0
 END-PROC
*
 TEST-INTEREST-HEADER. PROC

     IF PE002-INTEREST-SEGMENT-FORMAT-C EQ 100
      * IF PE010-INTEREST-HEADER-CHARGE-CD EQ 190
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
        IF PE045-INTEREST-MEMO-ITEM EQ 0, 1, 4, 5, 6
           INTEREST-OWING = PE604-INTEREST-EARNED-MTD + INTEREST-OWING
           W-INTEREST-FEE = W-INTEREST-FEE +               +
                            PE600-INTEREST-EARNED-ITD -    +
                            PE630-INTEREST-PAID-ITD
          *IF W-INTEREST-RATE EQ 0
          *   IF PE020-INTEREST-HEADER-EFF-FROM-DT LE W-ACCRUAL-DATE +
          *                        AND                               +
          *      PE030-INTEREST-HEADER-EFF-TO-DT GE W-ACCRUAL-DATE
          *         W-INTEREST-RATE = PE040-INTEREST-HEADER-RATE
          *   END-IF
          *END-IF
           IF PE604-INTEREST-EARNED-MTD NE 0
              ZERO-SW = 'N'
           END-IF
           W-INTEREST = PE730-INTEREST-BILLED-NOT-RECVD +      +
                                   PE606-INTEREST-EARNED-BTD
           IF PE730-INTEREST-BILLED-NOT-RECVD GT 0 AND +
                                   W-INTEREST GT 0
              CHARGE-CODE     = PE010-INTEREST-HEADER-CHARGE-CD
              BILLED-AMOUNT   = PE730-INTEREST-BILLED-NOT-RECVD
              PAST-DUE-AMOUNT = PE730-INTEREST-BILLED-NOT-RECVD
              PERFORM TEST-DELINQUENCY
           END-IF
         * IF PE510-INTEREST-PRIME-NUMBER NE 0
         *    RATE-CODE = 'V'
         * END-IF
        END-IF
     END-IF
 END-PROC
*
 TEST-FEE-HEADER. PROC

     IF PE002-INTEREST-SEGMENT-FORMAT-C EQ 400
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
 ****** IF PE010-INTEREST-HEADER-CHARGE-CD EQ 430
        IF PE045-INTEREST-MEMO-ITEM EQ 0, 1, 4, 5, 6
           FEES-OWING = PE604-INTEREST-EARNED-MTD + FEES-OWING
           W-INTEREST-FEE = W-INTEREST-FEE +               +
                            PE600-INTEREST-EARNED-ITD -    +
                            PE630-INTEREST-PAID-ITD
           IF PE604-INTEREST-EARNED-MTD NE 0
              ZERO-SW = 'N'
           END-IF
        END-IF
     END-IF
 END-PROC
*
 TEST-PRIME-SCHEDULES. PROC

     IF RK002-PRIM-SCH-SEGMENT-FORMAT-C EQ 915                          00009940
        RK-OCCRS = 1                                                    00010310
        IF RK004-PRIM-SCH-ACTIVE-ENTRIES GT 0                           00010320
       RK-BUCKET-LOOP.                                                  00010330
*          RKBKT-NDX = (RK-OCCRS - 1) * 40                              00010340
           RKBKT-NDX = (RK-OCCRS - 1) * 60                              00010340
          *IF W-INTEREST-RATE EQ 0                                      00010000
              IF RK020-PRIM-SCH-EFFECTIVE-FROM-DATE LE W-ACCRUAL-DATE + 00010010
                                   AND                               +  00010020
                 RK030-PRIM-SCH-EFFECTIVE-TO-DATE GE W-ACCRUAL-DATE     00010030
           IF RK040-PRIM-SCH-PRIME-NUMBER NE 0
              RATE-CODE = 'V'
           END-IF
              END-IF                                                    00010050
         * END-IF                                                       00010060
           IF RK-OCCRS LS RK004-PRIM-SCH-ACTIVE-ENTRIES                 00010540
              RK-OCCRS = RK-OCCRS + 1                                   00010550
              GO TO RK-BUCKET-LOOP                                      00010560
           END-IF                                                       00010570
        END-IF                                                          00010100
     END-IF                                                             00010110
 END-PROC
*
 EXTRACT-ROUTINE. PROC
     DATE-IN =  W-LAST-DAILY-TRAN
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
     DATE-IN2 = W-ZERO-BAL-PROC-D
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
 ****IF W-PRINCIPAL EQ 0 AND W-INTEREST-FEE EQ 0
 ****** IF MM-IN EQ MM-RPT AND YY-IN EQ YY-RPT AND DD-IN EQ DD-RPT
 ****** IF MM-IN EQ W-ZERO-BAL-MM AND YY-IN EQ W-ZERO-BAL-YY
        IF MM-RPT EQ W-ZERO-BAL-MM AND YY-RPT EQ W-ZERO-BAL-YY
           W-STATUS-CODE         =  'C'
        END-IF
 ****END-IF
    *IF ZERO-SW = 'Y'  AND  W-STATUS-CODE NE 'C'
    *   GO TO END-EXTRACT
    *END-IF
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
 ****IF PRINCIPAL-BALANCE NE 0
     IF PRINCIPAL-BALANCE NE 0 OR W-PRINCIPAL NE 0
        IF DESCRIPTION EQ '00000'
           DISPLAY 'NO MATCH ' W-BANK ' ' W-OBLIGOR ' ' W-OBLIGAT +
            '   RECORD CODE ' HOLD-RECORD-CODE '  GL CODE ' W-GL-CODE
        END-IF
        CRITERIA = 00163
        PRINT BAL-RPT
        W-AMOUNT = PRINCIPAL-BALANCE
        CRITERIA = 10041
      **PRINT BAL-RPT
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
 ****** IF PROCESS-TYPE-LEVEL NE 54, 56, 57, 59
        IF PROCESS-TYPE-LEVEL EQ 51, 52, 55, 58, 61
           PRINT BAL-RPT
 ****** CHANGES FOR JOHN RADA TO CREATE PMS EXTRACT
           IF RATE-CODE EQ 'F'
           EXT-CRITERIA2 = 20001
           EXT-BANK2     = W-BANK
           EXT-BRANCH2   = W-ASSIGNMENT
           EXT-OBLIGOR2  = W-OBLIGOR
           EXT-OBLIGAT2  = W-OBLIGAT
           EXT-TYPE2     = OBLIGATION-TYPE
           EXT-DATE2     = DATE-INC
           EXT-AMOUNT2   = PRINCIPAL-BALANCE
           EXT-MAT-DT2   = WORK-DATEA
           EXT-ORIG-DT2  = WORK-DATEB
           WORK-DATEA    = 0
           WORK-DATEB    = 0
           PUT ME7232
           END-IF
 ****** END CHANGES
        END-IF
      * W-AMOUNT = W-LOAN-LOSS
      * CRITERIA = 00562
      * PRINT BAL-RPT
        W-AMOUNT = 0
        IF W-SECURED-CODE EQ 2, 3
           CRITERIA = 00529
           PRINT BAL-RPT
        END-IF
        IF DELINQ-SW EQ 'Y'
           CRITERIA = 00505
           PRINT BAL-RPT
      *    DISPLAY ' DAYS DELINQUENT ' DIFF ' ' W-OBLIGOR ' ' W-OBLIGAT
        END-IF
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
        IF W-STATUS-CODE EQ 'N' AND W-ORIGINAL GT 0
           CRITERIA = 00501
           PRINT BAL-RPT
        END-IF
        MOVE CRITERIA TO CRITERIA
       ELSE
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
        IF W-STATUS-CODE EQ 'N' AND W-ORIGINAL GT 0
           CRITERIA = 00501
           PRINT BAL-RPT
        END-IF
        IF W-STATUS-CODE EQ 'C'
           CRITERIA = 00002
           PRINT BAL-RPT
        END-IF
     END-IF
     IF INTEREST-OWING NE 0
 ****** CHANGES TO MATCH MIKE SEMPLES STATISTICS
        IF PROCESS-TYPE-LEVEL NE 54, 56, 57, 59
           W-AMOUNT = INTEREST-OWING
           CRITERIA = 10058
           PRINT BAL-RPT
           W-AMOUNT = 0
        END-IF
     END-IF
     IF FEES-OWING GE 0
     END-IF
    END-EXTRACT
 END-PROC
*
 TITLE-DATE-PROC. PROC
      DATE-OUT   =     M-DATE
      MOVE MM-OUT TO MM-RPT
      MOVE DD-OUT TO DD-RPT
      MOVE YY-OUT TO YY-RPT
      MOVE MM-OUT TO BUS-MM
      MOVE YY-OUT TO BUS-YY
      IF YY-OUT LE 60
         BUS-CC = 20
        ELSE
         BUS-CC = 19
      END-IF
 END-PROC
*
 LMS-DATE-RTN. PROC
      MOVE MM-IN TO MM-OUT
      MOVE DD-IN TO DD-OUT
      MOVE YY-IN TO YY-OUT
 END-PROC
*
 TEST-DELINQUENCY. PROC
     IF TABLE-SW = 'Y'
        ITR-OCCRS = CURRENT-MAX-ITR-OCCRS
        GO TO READ-INVOICE-TABLE
     END-IF
     KEY-V-005-BANK-NUMBER     = W-BANK
     KEY-V-010-OBLIGOR-NUMBER  = W-OBLIGOR
     MOVE KEY-V-INVOICE-NUMBER     TO   KEY-V-013-INVOICE-NUMBER
     KEY-V-025-RECORD-CODE     = 00000
     POINT M8134D   GE KEY-V-KEY, STATUS
     IF EOF M8134D , OR FILE-STATUS (M8134D) NOT ZERO
            DISPLAY ' UNSUCCESSFUL POINT FOR THIS INVOICE    RECORD ' +
                    '  KEY = ' KEY-V-KEY
          * DISPLAY '      T-WORK    ' TRAN-WORK
        GO TO END-TEST-DELINQUENCY
     END-IF
     MOVE SPACES               TO  INVOICE-TABLE
     ITR-OCCRS = 1
    READ-INVOICE-LOOP
     GET   M8134D
     IF EOF M8134D , OR FILE-STATUS (M8134D) NOT ZERO
            DISPLAY ' UNSUCCESSFUL READ  FOR THIS INVOICE    RECORD ' +
                    '    KEY = ' KEY-V-KEY                +
                    '    FILE STATUS ' FILE-STATUS (M8134D)
           *DISPLAY '      T-WORK    ' TRAN-WORK
        GO TO END-TEST-DELINQUENCY
     END-IF
     IF VI010-OBLIGOR-NUMBER NE W-OBLIGOR
        GO TO READ-INVOICE-TABLE
     END-IF
     IF VI055-OBLIGATION-NUMBER NE W-OBLIGAT
        GO TO READ-INVOICE-LOOP
     END-IF
     IF VI070-INVOICE-BILLING-DATE EQ 9999999
        GO TO READ-INVOICE-LOOP
     END-IF
     ITR-SEG-NDX = (ITR-OCCRS - 1) * 65
     MOVE VI070-INVOICE-BILLING-DATE  TO  TVI070-INVOICE-BILLING-DATE
     MOVE VI072-INVOICE-BILLING-DUE-DATE  TO   +
                                         TVI072-INVOICE-BILLING-DUE-DATE
     MOVE VI-INVOICE-DATA             TO  TVI-INVOICE-DATA
     IF ITR-OCCRS LS 100
        ITR-OCCRS = ITR-OCCRS + 1
        GO TO READ-INVOICE-LOOP
     END-IF
    READ-INVOICE-TABLE
     TABLE-SW = 'Y'
     CURRENT-MAX-ITR-OCCRS = ITR-OCCRS
     TEST-AMOUNT         =   0
    READ-TABLE-LOOP
     ITR-SEG-NDX = (ITR-OCCRS - 1) * 65
     TVI-OCCRS = 1
    TVI-SEGMENT-LOOP.
     TVI-SEG-NDX = (TVI-OCCRS - 1) * 19
     IF TVI200-CHARGE-CODE  NUMERIC
        IF TVI200-CHARGE-CODE EQ CHARGE-CODE
            TEST-AMOUNT = TEST-AMOUNT + TVI220-INVC-AMOUNT-THIS-PERIOD
            IF TEST-AMOUNT GE BILLED-AMOUNT
                REPORT-PAST-DUE = BILLED-AMOUNT - TEST-AMOUNT +    +
                                        TVI220-INVC-AMOUNT-THIS-PERIOD
                DATE-IN = TVI072-INVOICE-BILLING-DUE-DATE
                PERFORM COMPUTE-DAYS-DIFF
                GO TO END-TEST-DELINQUENCY
               ELSE
                DATE-IN = TVI072-INVOICE-BILLING-DUE-DATE
                PERFORM CONVERT-DATE-PROC
                IF END-JULIAN LE START-JULIAN
                   PAST-DUE-AMOUNT = PAST-DUE-AMOUNT -      +
                                        TVI220-INVC-AMOUNT-THIS-PERIOD
                END-IF
            END-IF
        END-IF
     END-IF
     IF TVI-OCCRS LS 3
        TVI-OCCRS = TVI-OCCRS + 1
        GO TO TVI-SEGMENT-LOOP
     END-IF
     IF ITR-OCCRS GT 1
        ITR-OCCRS = ITR-OCCRS - 1
        GO TO READ-TABLE-LOOP
     END-IF
     DISPLAY ' INVOICES DO NOT TOTAL BILLED AMOUNT ' W-BANK ' ' +
          W-OBLIGOR ' ' W-OBLIGAT
     DISPLAY '                       BILLED AMOUNT = ' BILLED-AMOUNT +
            '   INVOICES TOTAL ' TEST-AMOUNT
     DATE-IN = TVI072-INVOICE-BILLING-DUE-DATE
     PERFORM COMPUTE-DAYS-DIFF
   END-TEST-DELINQUENCY
 END-PROC
*
 LAST-REC-PROC. PROC
  IF EXTRACT-SW EQ 'Y'
         EXTRACT-SW = 'N'
         PERFORM EXTRACT-ROUTINE
  END-IF
 END-PROC
*
 CONVERT-DATE-PROC. PROC
    PERFORM LMS-DATE-RTN
    GREGORIAN = DATE-OUT
    REQUEST-SW = 4
    ERROR-SW = 0
    PERFORM LNSDATE-RTN
    MOVE JULIAN TO START-JULIAN
   *DISPLAY ' JULIAN AND START-JULIAN ' JULIAN ' ' START-JULIAN
 END-PROC

 COMPUTE-DAYS-DIFF. PROC
    PERFORM CONVERT-DATE-PROC
    IF END-JULIAN LE START-JULIAN
      *DISPLAY ' PAYMENT SCHEDULES OVERLAP    DUE DATE ' DATE-OUT
       GO TO END-COMPUTE-DAYS-DIFF
    END-IF
    DIFF = END-JJJ - START-JJJ + 1
    LEAPS = START-YY * 25
    IF START-YY LS END-YY
      IF LEAPS = 0
        DIFF = DIFF + 366
       ELSE
        DIFF = DIFF + 365
      END-IF
    END-IF
    IF DIFF LS DELQ-CATEGORY1
       DELQ-LEVEL = 0
      ELSE
       IF DIFF LS DELQ-CATEGORY2
          DELQ-LEVEL = 1
         ELSE
          IF DIFF LS DELQ-CATEGORY3
             DELQ-LEVEL = 2
            ELSE
             IF DIFF LS DELQ-CATEGORY4
                DELQ-LEVEL = 3
               ELSE
                DELQ-LEVEL = 4
             END-IF
          END-IF
       END-IF
    END-IF
   *IF DELQ-LEVEL GT 2
    IF DIFF GT 30
       DELINQ-SW = 'Y'
 ******PRINT DELQ-RPT
    END-IF
   END-COMPUTE-DAYS-DIFF
 END-PROC
*
 LNSDATE-RTN. PROC
     CALL LNSDATE  USING (JULIAN GREGORIAN REQUEST-SW ERROR-SW)
     IF ERROR-SW NE 0
        DISPLAY ' LNSDATE ERROR ' JULIAN ' ' GREGORIAN ' ' REQUEST-SW
        STOP
     END-IF
 END-PROC
*
  REPORT DELQ-RPT SPACE 1
    SEQUENCE W-BANK W-DIVISION W-REGION W-ASSIGNMENT      +
               W-CONTACT W-OBLIGOR W-OBLIGAT CHARGE-CODE
   *           W-CONTACT DELQ-LEVEL W-OBLIGOR W-OBLIGAT CHARGE-CODE
    CONTROL  W-BANK RENUM                                 +
            W-DIVISION NOPRINT W-REGION NOPRINT W-ASSIGNMENT NOPRINT +
            W-CONTACT NEWPAGE
   *        W-CONTACT NEWPAGE DELQ-LEVEL
    SUM PRINCIPAL-BALANCE REPORT-PAST-DUE
    TITLE 'DELINQUENCY REPORT AS OF' ACCRUAL-DATE
    TITLE 2 'DELINQUENT PARAMETERS' DELQ-CATEGORY1 DELQ-CATEGORY2 +
              DELQ-CATEGORY3 DELQ-CATEGORY4
    TITLE 3 'BANK' W-BANK +2 'DIVISION' W-DIVISION +2 'REGION' +
              W-REGION +2 'UNIT' W-ASSIGNMENT +2 'OFFICER' W-CONTACT
  HEADING HOLD-PROCESS-TYPE ('PROC' 'TYPE')
  HEADING PRINCIPAL-BALANCE ('CURRENT' 'OUTSTANDING')
  HEADING INTEREST-OWING    ('TOTAL' 'INT/FEES DUE')
  HEADING W-INTEREST-FEE    ('TOTAL' 'INT/FEES DUE')
  HEADING CHARGE-CODE ('CHARGE' 'CODE')
  HEADING PAST-DUE-AMOUNT ('PAST DUE' 'AMOUNT')
  HEADING REPORT-PAST-DUE ('PAST DUE' 'AMOUNT')
  HEADING GREGORIAN     ('PAST DUE' 'DATE')
  HEADING DIFF          ('DAYS' 'OVRDU')
    LINE   W-OBLIGOR W-OBLIGAT HOLD-PROCESS-TYPE -1 PRINCIPAL-BALANCE +
         -1 W-INTEREST-FEE   CHARGE-CODE -1 REPORT-PAST-DUE  +
                                             GREGORIAN    DIFF TALLY
  * LINE   W-OBLIGOR W-OBLIGAT HOLD-PROCESS-TYPE -1 PRINCIPAL-BALANCE +
  *      -1 INTEREST-OWING   CHARGE-CODE -1 PAST-DUE-AMOUNT  +
  *                                          GREGORIAN    DIFF TALLY

  REPORT BAL-RPT SUMMARY SUMFILE SMYFIL2
   *SEQUENCE CRITERIA W-BANK W-ASSIGNMENT DESCRIPTION ACCR-DATE
   *CONTROL CRITERIA NOPRINT W-BANK NOPRINT W-ASSIGNMENT NOPRINT +
   *        DESCRIPTION NOPRINT ACCR-DATE NOPRINT
   *LINE    CRITERIA W-BANK W-ASSIGNMENT DESCRIPTION ACCR-DATE     +
    SEQUENCE CRITERIA W-BANK W-ASSIGNMENT WORK-TYPE ACCR-DATE
    CONTROL CRITERIA NOPRINT W-BANK NOPRINT W-ASSIGNMENT NOPRINT +
            WORK-TYPE NOPRINT ACCR-DATE NOPRINT
    LINE    CRITERIA W-BANK W-ASSIGNMENT WORK-TYPE ACCR-DATE     +
                                                      W-AMOUNT TALLY

*
JOB INPUT (SMYFIL KEY                                                 +
             (SMY-CRITERIA SMY-BANK SMY-BRANCH SMY-TYPE SMY-DATE)     +
           SMYFIL2 KEY                                                +
             (SMY2-CRITERIA SMY2-BANK SMY2-BRANCH SMY2-TYPE SMY2-DATE))

 IF MATCHED
   IF SMY-CRITERIA EQ 00002
      ACCOUNTS-PER-TYPE = ACCOUNTS-PER-TYPE + ACCOUNTS-PER-TYPE2
      PERFORM MOVE-SMYFIL
     ELSE
      DISPLAY ' ERROR ON MATCH  ' SMY-CRITERIA ' ' SMY-BRANCH SMY-TYPE
   END-IF
   GO TO JOB
 END-IF

 IF SMYFIL
   PERFORM MOVE-SMYFIL
   GO TO JOB
 END-IF

 IF SMYFIL2
   PERFORM MOVE-SMYFIL2
   GO TO JOB
 END-IF

 DISPLAY ' WHAT AM I DOING HERE????'

 MOVE-SMYFIL. PROC
  EXT-CRITERIA =  SMY-CRITERIA
  EXT-BANK     =  SMY-BANK
  EXT-BRANCH   =  SMY-BRANCH
  EXT-TYPE     =  SMY-TYPE
  EXT-DATE     =  SMY-DATE
  EXT-COUNT    =  ACCOUNTS-PER-TYPE
 *IF EXT-CRITERIA EQ 00559, 00560, 00562
  IF EXT-CRITERIA EQ 10058, 10041
     EXT-AMOUNT = SMY-AMOUNT
  END-IF
**IF EXT-COUNT GT 0
  IF EXT-COUNT NE 0
     PUT ME7231
  END-IF
 END-PROC

 MOVE-SMYFIL2. PROC
  EXT-CRITERIA =  SMY2-CRITERIA
  EXT-BANK     =  SMY2-BANK
  EXT-BRANCH   =  SMY2-BRANCH
  EXT-TYPE     =  SMY2-TYPE
  EXT-DATE     =  SMY2-DATE
  EXT-COUNT    =  ACCOUNTS-PER-TYPE2
 *IF EXT-CRITERIA EQ 00559, 00560, 00562
  IF EXT-CRITERIA EQ 10058, 10041
     EXT-AMOUNT = SMY2-AMOUNT
  END-IF
**IF EXT-COUNT GT 0
  IF EXT-COUNT NE 0
     PUT ME7231
  END-IF
 END-PROC
*
