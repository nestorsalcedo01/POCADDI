000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.  TRTMNT.
000300 AUTHOR. JON SAYLES.
000400 INSTALLATION. COBOL DEV Center.
000500 DATE-WRITTEN. 01/23/88.
000600 DATE-COMPILED. 01/23/88.
000700 SECURITY. CONFIDENTIAL PATIENT DATA.
000800
000900******************************************************************
001000*REMARKS.    change
001100*
001200*          THIS PROGRAM EDITS A DAILY TREATMENT TRANSACTION FILE
001300*          PRODUCED BY DATA ENTRY OPERATORS FROM CICS SCREENS
001400*
001500*          IT CONTAINS EVERY TREATMENT FOR EVERY PATIENT IN THE
001600*          HOSPITAL.
001700*
001800*          THE PROGRAM EDITS EACH RECORD AGAINST A NUMBER OF
001900*          CRITERIA, BALANCES FINAL TOTALS AND WRITES GOOD
002000*          RECORDS TO AN OUTPUT FILE
002100*
002200******************************************************************
002300
002400         INPUT FILE              -   DDS0001.TRMTDATA
002700
002800         INPUT ERROR FILE        -   DDS0001.TRMTERR
002900
003000         OUTPUT FILE PRODUCED    -   DDS001.TRMTEDIT
003100
003200         DUMP FILE               -   SYSOUT
003300
003400******************************************************************
003500
003600 ENVIRONMENT DIVISION.
003700 CONFIGURATION SECTION.
003800 SOURCE-COMPUTER. IBM-390.
003900 OBJECT-COMPUTER. IBM-390.
004000 INPUT-OUTPUT SECTION.
004100 FILE-CONTROL.
004200     SELECT SYSOUT
004300     ASSIGN TO UT-S-SYSOUT
004400       ORGANIZATION IS SEQUENTIAL.
004500
004600     SELECT TRMTDATA
004700     ASSIGN TO UT-S-TRMTDATA
004800       ACCESS MODE IS SEQUENTIAL
004900       FILE STATUS IS OFCODE.
005000
005100     SELECT TRMTEDIT
005200     ASSIGN TO UT-S-TRMTEDIT
005300       ACCESS MODE IS SEQUENTIAL
005400       FILE STATUS IS OFCODE.
005500
005600     SELECT TRMTERR
005700     ASSIGN TO UT-S-TRMTERR
005800       ACCESS MODE IS SEQUENTIAL
005900       FILE STATUS IS OFCODE.
006000
006700
006800 DATA DIVISION.
006900 FILE SECTION.
007000 FD  SYSOUT
007100     RECORDING MODE IS F
007200     LABEL RECORDS ARE STANDARD
007300     RECORD CONTAINS 130 CHARACTERS
007400     BLOCK CONTAINS 0 RECORDS
007500     DATA RECORD IS SYSOUT-REC.
007600 01  SYSOUT-REC  PIC X(130).
007700
007800****** THIS FILE IS PASSED IN FROM THE DATA COLLECTIONS SYSTEM
007900****** IT CONSISTS OF ALL PATIENT TREATMENTS ENTERED
008000****** THERE ARE TWO RECORD FORMATS - DETAIL AND TRAILER RECS
008100****** OUT OF BALANCE CONDITIONS SHOULD CAUSE THE JOB TO ABEND
008200 FD  TRMTDATA
008300     RECORDING MODE IS F
008400     LABEL RECORDS ARE STANDARD
008500     RECORD CONTAINS 1101 CHARACTERS
008600     BLOCK CONTAINS 0 RECORDS
008700     DATA RECORD IS INPATIENT-TREATMENT-REC-DATA.
008800 01  INPATIENT-TREATMENT-REC-DATA PIC X(1101).
008900
009000****** THIS FILE IS WRITTEN FOR ALL TREATMENT RECORDS THAT PASS
009100****** THE PROGRAM'S EDIT ROUTINES
009200****** THE TRAILER RECORD SHOULD ONLY CARRY THE NUMBER OF
009300****** RECORDS IN THE FILE ON TO THE NEXT JOB STEP
009400 FD  TRMTEDIT
009500     RECORDING MODE IS F
009600     LABEL RECORDS ARE STANDARD
009700     BLOCK CONTAINS 0 RECORDS
009800     RECORD CONTAINS 1101 CHARACTERS
009900     DATA RECORD IS INPATIENT-TREATMENT-REC-EDIT.
010000 01  INPATIENT-TREATMENT-REC-EDIT PIC X(1101).
010100
010200 FD  TRMTERR
010300     RECORDING MODE IS F
010400     LABEL RECORDS ARE STANDARD
010500     RECORD CONTAINS 1141 CHARACTERS
010600     BLOCK CONTAINS 0 RECORDS
010700     DATA RECORD IS INPATIENT-TREATMENT-REC-ERR.
010800 01  INPATIENT-TREATMENT-REC-ERR.
010900     05  ERR-MSG                     PIC X(40).
011000     05  REST-OF-REC                 PIC X(1101).
011100
011800
011900** QSAM FILE
012000 WORKING-STORAGE SECTION.
012100 01  FILLER                     PIC X(33) VALUE
012200        '* WORKING STORAGE BEGINS HERE *'.
012300
012400 01  FILLER                     PIC X(33) VALUE
012500             '****** DUMP MSG ****************'.
012600*****************************************************************
012700*    DUMP POINTER AREA1
012800*        PARA POINTER- MOVE PARAGRAPH NUMBER TO THIS POINTER    *
012900*                      AS EACH PARAGRAPH IS ENTERED. DO NOT     *
013000*                      MOVE PARAGRAPH NUMBERS OF COMMON
013100*                      PARAGRAPHS (USE COMM POINTER).
013200*                                                               *
013300*        COMM POINTER - EACH COMMON PARAGRAPH SHOULD MOVE       *
013400*                       ITS PARAGRAPH NUMBER TO THIS POINTER    *
013500*                       AT IT INCEPTION.
013600*                                                               *
013700*****************************************************************
013800 01  DUMP-LOCATOR.
013900     05 FILLER             PIC X(32)
014000         VALUE '>>>>>>> WS DUMP POINTERS >>>>>>>'.
014100     05 FILLER             PIC X(16)   VALUE 'Z PARA POINTER'.
014200     05 PARA-POINTER       PIC X(8)    VALUE SPACES.
014300     05 FILLER             PIC X(8)    VALUE '       Z'.
014400     05 FILLER             PIC X(16)   VALUE 'Z COMM POINTER'.
014500     05 COMM-POINTER       PIC X(8)    VALUE SPACES.
014600     05 FILLER             PIC X(32)
014700             VALUE '<<<<<<< WS DUMP POINTERS <<<<<<<'.
014800
014900 01  WS-CALLED-MODULE   PIC X(8) VALUE "DTEVAL".
015000 01  DUMP-DISPLAY.
015100     05 DUMP-STATUS               PIC X(3)  VALUE SPACES.
015200     05 DUMP-MESSAGE              PIC X(61) VALUE 'NO MSG'.
015300
015400 01  FILE-STATUS-CODES.
015500     05  PATMSTR-STATUS          PIC X(2).
015600         88 RECORD-FOUND         VALUE "00".
015700         88 PATMSTR-NOT-FOUND    VALUE "23".
015800     05  OFCODE                  PIC X(2).
015900         88 CODE-WRITE    VALUE SPACES.
016000
016100 COPY TREATMNT.
016200
016300 01  WS-TRAILER-REC.
016400     05  FILLER                  PIC X(1).
016500     05  IN-RECORD-COUNT         PIC 9(9).
016600     05  FILLER                  PIC X(1).
016700     05  IN-MEDICATION-CHARGES   PIC S9(9)V99.
016800     05  IN-PHARMACY-CHARGES     PIC S9(7)V99.
016900     05  IN-ANCILLARY-CHARGES    PIC S9(5)V99.
017000
017100 01  WS-OUTPUT-REC.
017200     05  PATIENT-NBR-O           PIC 9(6).
017300     05  FILLER                  PIC X(2) VALUE SPACES.
017400     05  PATIENT-NAME-O          PIC X(20).
017500     05  PATIENT-PHONE-O         PIC X(10).
017600     05  FILLER                  PIC X(2) VALUE SPACES.
017700     05  PATIENT-TYPE-O          PIC X(2).
017800     05  BED-IDENTITY-O          PIC ZZZ9.
017900     05  FILLER                  PIC X(2) VALUE SPACES.
018000     05  CURR-DATE-O             PIC X(6).
018100     05  FILLER                  PIC X(2) VALUE SPACES.
018200     05  PATIENT-AMT-PER-DAY-O   PIC $$,$$9.99.
018300     05  FILLER                  PIC X(2) VALUE SPACES.
018400     05  INS-COVERAGE-PERC-O     PIC 999.
018500     05  FILLER                  PIC X(2) VALUE SPACES.
018600     05  INS-TYPE-O              PIC X(4).
018700     05  HOSPITAL-STAY-LTH-O     PIC 999.
018800     05  FILLER                  PIC X(7) VALUE SPACES.
018900
019000     COPY PATMSTR.
019100** VSAM FILE
019200
019300 01  WS-SYSOUT-REC.
019400     05  MSG                     PIC X(80).
019500
019600 77  WS-DATE                     PIC 9(6).
019700
019800 01  COUNTERS-AND-ACCUMULATORS.
019900     05 RECORDS-WRITTEN          PIC 9(7) COMP.
020000     05 RECORDS-IN-ERROR         PIC 9(7) COMP.
020100     05 RECORDS-READ             PIC 9(7) COMP.
020200     05 WS-MEDICATION-CHARGES    PIC S9(9)V99 COMP-3.
020300     05 WS-PHARMACY-CHARGES      PIC S9(6)V99 COMP-3.
020400     05 WS-ANCILLARY-CHARGES     PIC S9(4)V99 COMP-3.
020500
020600 01  MISC-WS-FLDS.
020700     05 STR-LTH                  PIC 9(04) VALUE 0.
020800     05 RETURN-CD                PIC S9(04) VALUE 0.
020900     05 ROW-SUB                  PIC S9(02) COMP-3.
021000
021100 01  FLAGS-AND-SWITCHES.
021200     05 MORE-DATA-SW             PIC X(01) VALUE "Y".
021300         88 NO-MORE-DATA VALUE "N".
021400     05 ERROR-FOUND-SW           PIC X(01) VALUE "N".
021500         88 RECORD-ERROR-FOUND VALUE "Y".
021600         88 VALID-RECORD  VALUE "N".
021600         88 VALID-ROW  VALUE    "R".
021700     05  MORE-TABLE-ROWS         PIC X(01) VALUE "Y".
021800         88 NO-MORE-TABLE-ROWS VALUE "N".
021900
022000* COPY ABENDREC.
022100** QSAM FILE
022200 COPY ABENDREC.
022300* COPY BADD400.
022400
022500* COPY DIAGCODE.
022600******************************************************************
022700***** DB2 TABLE DCLGENS
022800 01  DCLDIAG-CODES.
022900     10 DIAG-CODE                      PIC X(05).
023000     10 INS-TYPE                       PIC X(03).
023100     10 COPAY                          PIC S9(4) COMP.
023200     10 DEDUCTIBLE                     PIC S9(4) COMP.
023300
023400 01  DCLWARD-CODES.
023500     10 WARD-ID                        PIC X(04).
023600     10 PRIMARY-PHYSICIAN-ID           PIC X(08).
023700     10 SUPERVISE-NURSE-ID             PIC X(08).
023800     10 LOCATION                       PIC X(08).
023900     10 NUMBER-OF-BEDS                 PIC S9(4) COMP.
024000     10 BASE-ROOM-CHARGE               PIC S9(5)V99 COMP-3.
024100
024200 01  DCLHOSP-BED.
024300     10 BED-ID                         PIC X(04).
024400     10 ROOM-ID                        PIC X(08).
024500     10 WARD-ID                        PIC X(08).
024600     10 SPECIAL-CHARGES                PIC S9(5)V99 COMP-3.
024700
024800 01  DCLMEDICATION.
024900     10 MEDICATION-ID                  PIC X(08).
025000     10 MED-NAME                       PIC X(08).
025100     10 SHORT-DESCRIPTION              PIC X(08).
025200     10 COST                           PIC S9(5)V99 COMP-3.
025300     10 PHARMACY-COST                  PIC S9(3)V99 COMP-3.
025400
025500     EXEC SQL INCLUDE  SQLCA END-EXEC.
025600
025700 PROCEDURE DIVISION.
025800     PERFORM 000-HOUSEKEEPING THRU 000-EXIT.
025900     PERFORM 100-MAINLINE THRU 100-EXIT
026000             UNTIL NO-MORE-DATA OR
026100******* Balancing logic put in by TGD 02/12/92
026200             TRAILER-REC.
026300     PERFORM 999-CLEANUP THRU 999-EXIT.
026400     MOVE +0 TO RETURN-CODE.
026500     GOBACK.
026600
026700 000-HOUSEKEEPING.
026800     MOVE "000-HOUSEKEEPING" TO PARA-NAME.
026900     DISPLAY "HOUSEKEEPING".
027000*  Code your statement here to OPEN files
027100     ACCEPT  WS-DATE FROM DATE.
027200     INITIALIZE COUNTERS-AND-ACCUMULATORS.
027300     PERFORM 800-OPEN-FILES THRU 800-EXIT.
027400     PERFORM 900-READ-TRMTDATA THRU 900-EXIT.
027500     DISPLAY 'MORE-DATA-SW ' MORE-DATA-SW.
027600     IF NO-MORE-DATA
027700         MOVE "EMPTY INPUT FILE" TO ABEND-REASON
027800         GO TO 1000-ABEND-RTN.
027900 000-EXIT.
028000     EXIT.
028100
028200 100-MAINLINE.
028300     DISPLAY '100-MAINLINE...JS'.
           DISPLAY 'TRMT REC ' INPATIENT-TREATMENT-REC.
028400     MOVE "100-MAINLINE" TO PARA-NAME.
028500*  Validate patient type and insurance coverage
028600     PERFORM 300-FIELD-EDITS THRU 300-EXIT.
028700
028800     IF RECORD-ERROR-FOUND
028900         ADD +1 TO RECORDS-IN-ERROR
029000         PERFORM 710-WRITE-TRMTERR THRU 710-EXIT
029100     ELSE
029200         PERFORM 700-WRITE-TRMTEDIT THRU 700-EXIT.
029300     PERFORM 900-READ-TRMTDATA THRU 900-EXIT.
029400 100-EXIT.
029500     EXIT.
029600
029700 300-FIELD-EDITS.
029800     MOVE "N" TO ERROR-FOUND-SW IN FLAGS-AND-SWITCHES.
029900     MOVE "300-FIELD-EDITS" TO PARA-NAME.
030000******** non-numeric fields
030100     IF NOT VALID-BILLABLE-TYPES IN BILLABLE-TREATMENT-IND
030200        MOVE "*** INVALID BILLABLE TYPE" TO
030300        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
030400        MOVE "Y" TO ERROR-FOUND-SW
030500        GO TO 300-EXIT.
030600
030700     IF NOT VALID-TRTMNT-MODES IN TREATMENT-MODE
030800        MOVE "*** INVALID TREATMENT MODE" TO
030900        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
031000        MOVE "Y" TO ERROR-FOUND-SW
031100        GO TO 300-EXIT.
031200
031300     IF PATIENT-ID IN INPATIENT-TREATMENT-REC NOT NUMERIC
031400        MOVE "*** NON-NUMERIC PATIENT-ID" TO
031500        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
031600        MOVE "Y" TO ERROR-FOUND-SW
031700        GO TO 300-EXIT.
031800
031900     IF PATIENT-ID IN INPATIENT-TREATMENT-REC = ZERO
032000        MOVE "*** INVALID (000000) PATIENT-ID" TO
032100        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
032200        MOVE "Y" TO ERROR-FOUND-SW
032300        GO TO 300-EXIT.
032400
032500     IF BED-IDENTITY IN INPATIENT-TREATMENT-REC NOT NUMERIC
032600        MOVE "*** NON-NUMERIC BED-IDENTITY" TO
032700        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
032800        MOVE "Y" TO ERROR-FOUND-SW
032900        GO TO 300-EXIT.
033000
033100     IF MEDICATION-COST IN INPATIENT-TREATMENT-REC NOT NUMERIC
033200        MOVE "*** NON-NUMERIC MEDICATION-COST" TO
033300        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
033400        MOVE "Y" TO ERROR-FOUND-SW
033500        GO TO 300-EXIT.
033600
033700     IF PHARMACY-COST IN INPATIENT-TREATMENT-REC NOT NUMERIC
033800        MOVE "*** NON-NUMERIC PHARMACY COSTS" TO
033900        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
034000        MOVE "Y" TO ERROR-FOUND-SW
034100        GO TO 300-EXIT.
034200
034300     IF ANCILLARY-CHARGE IN INPATIENT-TREATMENT-REC NOT NUMERIC
034400        MOVE "*** NON-NUMERIC ANCILLARY-CHARGES" TO
034500        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
034600        MOVE "Y" TO ERROR-FOUND-SW
034700        GO TO 300-EXIT.
034800********************
034900     IF ATTENDING-PHYS-ID = SPACES
035000        MOVE "*** BLANK ATTENDING PHYSICIAN-ID" TO
035100        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
035200        MOVE "Y" TO ERROR-FOUND-SW
035300        GO TO 300-EXIT.
035400
035500     IF PRESCRIBING-PHYS-ID = SPACES
035600        MOVE "*** BLANK PRESCRIBING PHYSICIAN-ID" TO
035700        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
035800        MOVE "Y" TO ERROR-FOUND-SW
035900        GO TO 300-EXIT.
036000
036100*    CALL WS-CALLED-MODULE USING TREATMENT-DATE, RETURN-CD.
036200*     CALL 'DTEVAL' USING TREATMENT-DATE, RETURN-CD.
036300*     CALL 'DATEVAL' USING TREATMENT-DATE, RETURN-CD.
036400     IF RETURN-CD < 0
036500        MOVE "*** BAD DATE PORTION OF DATE-TIME" TO
036600        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
036700        MOVE "Y" TO ERROR-FOUND-SW
036800        GO TO 300-EXIT.
036900
037000     MOVE "Y" TO MORE-TABLE-ROWS.
037100     PERFORM 350-CHECK-LAB-TABLE THRU 350-EXIT VARYING ROW-SUB
037200          FROM 1 BY 1 UNTIL NO-MORE-TABLE-ROWS OR ROW-SUB = 8.
037300
037400     IF VALID-RECORD
037500         PERFORM 400-NUMERIC-RANGE-EDITS THRU 400-EXIT.
037600
037700****** VERIFY TABLE (JUST TYPES AND LAB-TEST-ID)
037800
037900 300-EXIT.
038000     EXIT.
038100
038200 350-CHECK-LAB-TABLE.
038300     IF LAB-TEST-ID(ROW-SUB) = SPACES
038400        MOVE "N" TO MORE-TABLE-ROWS
038500        GO TO 350-EXIT.
038600
038700     IF NOT VALID-CATEGORY(ROW-SUB)
038800        MOVE "*** INVALID LAB-TEST CATEGORY" TO
038900        ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
039000        MOVE "Y" TO ERROR-FOUND-SW
039100        GO TO 350-EXIT.
039200
039300 350-EXIT.
039400     EXIT.
039500
039600
039700 400-NUMERIC-RANGE-EDITS.
039800     MOVE "400-NUMERIC-RANGE-EDITS" TO PARA-NAME.
039900******** Call to VSAM file to read record
040000     IF  (MEDICATION-COST > 99000
040100     OR  MEDICATION-COST < 1.01)
040200         MOVE "*** INVALID MEDICATION COST" TO
040300         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
040400         MOVE "Y" TO ERROR-FOUND-SW
040500         GO TO 400-EXIT.
040600
040700     IF  (PHARMACY-COST IN INPATIENT-TREATMENT-REC > 990
040800     OR  PHARMACY-COST IN INPATIENT-TREATMENT-REC < .99)
040900         MOVE "*** INVALID PHARMACY COSTS" TO
041000         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
041100         MOVE "Y" TO ERROR-FOUND-SW
041200         GO TO 400-EXIT.
041300
041400     IF  (ANCILLARY-CHARGE > 900
041500     OR  ANCILLARY-CHARGE < 1.01)
041600         MOVE "*** INVALID ANCILLARY CHARGES" TO
041700         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
041800         MOVE "Y" TO ERROR-FOUND-SW
041900         GO TO 400-EXIT.
042000
042100     IF VALID-RECORD
042200         PERFORM 450-CROSS-FIELD-EDITS THRU 450-EXIT.
042300
042400 400-EXIT.
042500     EXIT.
042600
042700 450-CROSS-FIELD-EDITS.
042800     MOVE "450-CROSS-FIELD-EDITS" TO PARA-NAME.
042900******** Specific requirements for certain frocedures
043000     IF  MRI OR CAT OR CHEMO-THERAPY OR RADIATION-THERAPY
043100         OR SURGERY OR LAB-TESTS
043200         IF MEDICATION-COST = ZERO OR
043300            ANCILLARY-CHARGE = ZERO
043400         MOVE "*** INVALID $$ AMOUNTS FOR fROCEDURES" TO
043500         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
043600         MOVE "Y" TO ERROR-FOUND-SW
043700         GO TO 450-EXIT.
043800
043900     IF  ORAL-ADMIN OR INTRAVENOUS-ADMIN OR INJECTION
044000         IF PHARMACY-COST IN INPATIENT-TREATMENT-REC = ZERO OR
044100            ANCILLARY-CHARGE = ZERO
044200         MOVE "*** INVALID $$ AMOUNTS FOR PROCEDURES" TO
044300         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
044400         MOVE "Y" TO ERROR-FOUND-SW
044500         GO TO 450-EXIT.
044600
044700     IF  NOT OTHER-TREATMENT
044800         IF TREATMENT-NURSE-ID = SPACES OR
044900            SUPERVISOR-NURSE-ID = SPACES
045000         MOVE "*** INVALID NURSING ENTRIES" TO
045100         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
045200         MOVE "Y" TO ERROR-FOUND-SW
045300         GO TO 450-EXIT.
045400
045500     IF  NOT (OTHER-TREATMENT AND LAB-TESTS)
045600         IF TREATMENT-COMMENTS = SPACES
045700         MOVE "*** INVALID TREATMENT COMMENTS" TO
045800         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
045900         MOVE "Y" TO ERROR-FOUND-SW
046000         GO TO 450-EXIT.
046100
046200     IF  CHEMO-THERAPY OR RADIATION-THERAPY OR SURGERY
046300        MOVE +0 TO STR-LTH
046400*        CALL 'LENPGM' USING TREATMENT-COMMENTS, STR-LTH
046500        IF STR-LTH < 25
046600         MOVE "*** INVALID TREATMENT COMMENT LENGTH" TO
046700         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
046800         MOVE "Y" TO ERROR-FOUND-SW
046900         GO TO 450-EXIT.
047000
047100     IF VALID-RECORD
047200         PERFORM 500-CROSS-FILE-EDITS THRU 500-EXIT.
047300
047400 450-EXIT.
047500     EXIT.
047600
047700 500-CROSS-FILE-EDITS.
047800     MOVE "500-CROSS-FILE-EDITS" TO PARA-NAME.
048800     MOVE 'DTEVAL' TO WS-CALLED-MODULE.
047900******** Call to VSAM file to read record
           MOVE +0 TO RETURN-CD.
           MOVE 'N' to ERROR-FOUND-SW.

036100*     CALL WS-CALLED-MODULE USING TREATMENT-DATE, RETURN-CD.

048900     IF VALID-RECORD
049000        PERFORM 600-DB2-TABLE-EDITS THRU 600-EXIT.
049100
049200 500-EXIT.
049300     EXIT.
049400
049500 600-DB2-TABLE-EDITS.
049600     MOVE "600-DB2-TABLE-EDITS" TO PARA-NAME.
049700******** EXEC SQL to get info from DB2
049800     MOVE DIAGNOSTIC-CODE-PRIMARY IN PATIENT-MASTER-REC TO
049900          DIAG-CODE IN DCLDIAG-CODES.
050000
050100****** CHECK FOR VALID DIAGNOSTIC CODE
050200     EXEC SQL
050300        SELECT DIAG_CODE INTO :DIAG-CODE
050400        FROM DDS0001.DIAG_CODES
050500        WHERE DIAG_CODE = :DCLDIAG-CODES.DIAG-CODE
050600     END-EXEC.
050700
050800     IF SQLCODE = -811 OR 0
050900         NEXT SENTENCE
051000     ELSE
051100     IF SQLCODE = +100
051200         MOVE "*** DIAGNOSTIC CODE NOT-FOUND IN DIAG_CODES" TO
051300         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
051400         MOVE "Y" TO ERROR-FOUND-SW
051500         MOVE SQLCODE TO  PATIENT-ID IN INPATIENT-TREATMENT-REC
051600         MOVE DIAG-CODE IN DCLDIAG-CODES
051700                            TO PRIMARY-DIAGNOSTIC-CODE
051800         MOVE SQLCODE TO  EXPECTED-VAL
051900         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
052000                         TO ACTUAL-VAL
052100         WRITE SYSOUT-REC FROM ABEND-REC
052200         GO TO 600-EXIT
052300     ELSE
052400     IF SQLCODE < 0
052500         MOVE "***  FATAL DB2 ERROR HERE" TO
052600         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
052700         MOVE "Y" TO ERROR-FOUND-SW
052800         MOVE SQLCODE TO  PATIENT-ID IN INPATIENT-TREATMENT-REC
052900         MOVE DIAG-CODE IN DCLDIAG-CODES
053000                            TO PRIMARY-DIAGNOSTIC-CODE
053100         MOVE SQLCODE TO  EXPECTED-VAL
053200         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
053300                         TO ACTUAL-VAL
053400         WRITE SYSOUT-REC FROM ABEND-REC
053500         GO TO 1000-DB2-ERROR-RTN.
053600
053700****** CHECK FOR VALID BED IDENTITY
053800     MOVE BED-IDENTITY TO BED-ID.
053900     EXEC SQL
054000        SELECT BED_ID INTO :BED-ID
054100        FROM DDS0001.HOSP_BED
054200        WHERE BED_ID = :BED-ID
054300     END-EXEC.
054400
054500     IF SQLCODE = -811 OR 0
054600         NEXT SENTENCE
054700     ELSE
054800     IF SQLCODE = +100
054900         MOVE "*** BED IDENT NOT-FOUND IN HOSP_BED" TO
055000         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
055100         MOVE "Y" TO ERROR-FOUND-SW
055200         MOVE SQLCODE TO  EXPECTED-VAL
055300         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
055400                         TO ACTUAL-VAL
055500         WRITE SYSOUT-REC FROM ABEND-REC
055600         GO TO 600-EXIT
055700     ELSE
055800     IF SQLCODE < 0
055900         MOVE "***  FATAL DB2 ERROR" TO
056000         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
056100         MOVE "Y" TO ERROR-FOUND-SW
056200         MOVE SQLCODE TO  EXPECTED-VAL
056300         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
056400                         TO ACTUAL-VAL
056500         WRITE SYSOUT-REC FROM ABEND-REC
056600         GO TO 1000-DB2-ERROR-RTN
056700     ELSE
           IF SQLCODE = -803 PERFORM 852-WRITE-DUP-IDX-ERR
                   THRU 852-EXIT.
056800****** CHECK FOR VALID PHYSICIAN-ID
056900     MOVE ATTENDING-PHYS-ID TO PRIMARY-PHYSICIAN-ID.
057000     EXEC SQL
057100        SELECT PRIMARY_PHYSICIAN_ID INTO :PRIMARY-PHYSICIAN-ID
057200        FROM DDS0001.WARD_DATA
057300        WHERE PRIMARY_PHYSICIAN_ID = :PRIMARY-PHYSICIAN-ID
057500     END-EXEC.
057600
057700     IF SQLCODE = -811 OR 0
057800         NEXT SENTENCE
057900     ELSE
058000     IF SQLCODE = +100
058100         MOVE "*** ATTENDING PHYSICIAN NOT FOUND IN TABLE" TO
058200         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
058300         MOVE "Y" TO ERROR-FOUND-SW
058400         MOVE SQLCODE TO  EXPECTED-VAL
058500         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
058600                         TO ACTUAL-VAL
058700         WRITE SYSOUT-REC FROM ABEND-REC
058800         GO TO 600-EXIT
058900     ELSE
059000         MOVE "***  FATAL DB2 ERROR" TO
059100         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
059200         MOVE "Y" TO ERROR-FOUND-SW
059300         MOVE SQLCODE TO  EXPECTED-VAL
059400         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
059500                         TO ACTUAL-VAL
059600         WRITE SYSOUT-REC FROM ABEND-REC
059700         GO TO 1000-DB2-ERROR-RTN.
059800
059900****** CHECK FOR VALID MEDICATION-ID
060000     MOVE MEDICATION-ID IN INPATIENT-TREATMENT-REC TO
060100            MEDICATION-ID IN DCLMEDICATION.
060200
060300     EXEC SQL
060400        SELECT MEDICATION_ID
060500                       INTO :DCLMEDICATION.MEDICATION-ID
060600        FROM DDS0001.MEDICATION
060700        WHERE MEDICATION_ID = :DCLMEDICATION.MEDICATION-ID
060800     END-EXEC.
060900***********************
061000     IF SQLCODE = -811 OR 0
061100         NEXT SENTENCE
061200     ELSE
061300     IF SQLCODE = +100
061400         MOVE "*** MEDICATION-ID NOT FOUND IN TABLE" TO
061500         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
061600         MOVE "Y" TO ERROR-FOUND-SW
061700         MOVE SQLCODE TO  EXPECTED-VAL
061800         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
061900                         TO ACTUAL-VAL
062000         WRITE SYSOUT-REC FROM ABEND-REC
062100         GO TO 600-EXIT
062200     ELSE
062300     IF SQLCODE < 0
062400         MOVE "***  FATAL DB2 ERROR" TO
062500         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
062600         MOVE "Y" TO ERROR-FOUND-SW
062700         MOVE SQLCODE TO  EXPECTED-VAL
062800         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
062900                         TO ACTUAL-VAL
063000         WRITE SYSOUT-REC FROM ABEND-REC
063100         GO TO 1000-DB2-ERROR-RTN.
063200
063300****** CHECK FOR VALID SUPERVISOR NURSE-ID
063400     MOVE SUPERVISOR-NURSE-ID TO SUPERVISE-NURSE-ID.
063500     EXEC SQL
063600        SELECT SUPERVISE_NURSE_ID
063700                       INTO :SUPERVISE-NURSE-ID
063800        FROM DDS0001.WARD_DATA
063900        WHERE SUPERVISE_NURSE_ID = :SUPERVISE-NURSE-ID
064000     END-EXEC.
064100
064200     IF SQLCODE = -811 OR 0
064300         NEXT SENTENCE
064400     ELSE
064500     IF SQLCODE = +100
064600         MOVE "*** SUPERVISOR NURSE NOT FOUND" TO
064700         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
064800         MOVE "Y" TO ERROR-FOUND-SW
064900         MOVE SQLCODE TO  EXPECTED-VAL
065000         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
065100                         TO ACTUAL-VAL
065200         WRITE SYSOUT-REC FROM ABEND-REC
065300         GO TO 600-EXIT
065400     ELSE
065500     IF SQLCODE < 0
065600         MOVE "*** FATAL DB2 ERROR" TO
065700         ERR-MSG IN INPATIENT-TREATMENT-REC-ERR
065800         MOVE "Y" TO ERROR-FOUND-SW
065900         MOVE SQLCODE TO  EXPECTED-VAL
066000         MOVE PATIENT-ID IN INPATIENT-TREATMENT-REC
066100                         TO ACTUAL-VAL
066200         WRITE SYSOUT-REC FROM ABEND-REC
066300         GO TO 1000-DB2-ERROR-RTN.
066400 600-EXIT.
066500     EXIT.
066600
066700 700-WRITE-TRMTEDIT.
066800     MOVE "700-WRITE-TRMTEDIT" TO PARA-NAME.
066900
067000     WRITE INPATIENT-TREATMENT-REC-EDIT
067100         FROM INPATIENT-TREATMENT-REC.
067200     ADD MEDICATION-COST  TO WS-MEDICATION-CHARGES.
067300     ADD ANCILLARY-CHARGE TO WS-ANCILLARY-CHARGES.
067400     ADD PHARMACY-COST IN INPATIENT-TREATMENT-REC
067500                          TO WS-PHARMACY-CHARGES.
067600     ADD +1 TO RECORDS-WRITTEN.
067700 700-EXIT.
067800     EXIT.
067900
068000 710-WRITE-TRMTERR.
068100     MOVE INPATIENT-TREATMENT-REC TO REST-OF-REC.
068200     WRITE INPATIENT-TREATMENT-REC-ERR.
068300     ADD +1 TO RECORDS-IN-ERROR.
068400 710-EXIT.
068500     EXIT.
068600
068700 800-OPEN-FILES.
068800     DISPLAY '800-OPEN-FILES...'.
068900     MOVE "800-OPEN-FILES" TO PARA-NAME.
069000     OPEN INPUT TRMTDATA.
069100     OPEN OUTPUT TRMTEDIT, SYSOUT, TRMTERR.
069300 800-EXIT.
069400     EXIT.
069500
069600 850-CLOSE-FILES.
069700     MOVE "850-CLOSE-FILES" TO PARA-NAME.
069800     CLOSE TRMTDATA,
069900           TRMTEDIT, SYSOUT, TRMTERR.
070100 850-EXIT.
070200     EXIT.
070300
069600 851-WRITE-AUDIT-LOGS.
069700     MOVE "851-CLOSE-FILES" TO PARA-NAME.
           WRITE SYSOUT-REC AFTER ADVANCING 3.
      ***** END LOG/AUDIT TRAIL
070100 851-EXIT.
070200     EXIT.
070300
069600 852-WRITE-DUP-IDX-ERR.
      *    IF SQLCODE = -803 GO TO 852-EXIT.
069700*    MOVE "852-WRITE-DUP-IDX-ERR" TO PARA-NAME.
      *    WRITE SYSOUT-REC AFTER ADVANCING 1.
      ***** END LOG/AUDIT TRAIL
070100 852-EXIT.
070200     EXIT.
070300
069600 853-CLOSE-PGM.
      *    IF SQLCODE = -803 GO TO 852-WRITE-DUP-IDX-ERR.
069700     MOVE "853-WRITE-DUP-IDX-ERR" TO PARA-NAME.
      *    GOBACK.
           WRITE SYSOUT-REC AFTER ADVANCING 1.
      ***** END LOG/AUDIT TRAIL
070100 853-EXIT.
070200     EXIT.
070300
070400 900-READ-TRMTDATA.
070500     DISPLAY '900-READ-TRMTDATA...'.
070600*  Code your statements here to read the input file
070700*  Remember to move "NO" to IFCODE if the input file is AT END
070800     READ TRMTDATA  INTO INPATIENT-TREATMENT-REC
070900         AT END MOVE "N" TO MORE-DATA-SW
071000         GO TO 900-EXIT
071100     END-READ
071200     MOVE "N" TO ERROR-FOUND-SW.
071300     ADD +1 TO RECORDS-READ.
071400     DISPLAY '900.1 RECORDS-READ = ' RECORDS-READ.
071500 900-EXIT.
071600     EXIT.
071700
071800 999-CLEANUP.
071900     MOVE "999-CLEANUP" TO PARA-NAME.
072000*  Final file-handling edits and trailer record handling
072100     IF NOT TRAILER-REC
072200         MOVE "** INVALID FILE - NO TRAILER REC" TO ABEND-REASON
072300         GO TO 1000-ABEND-RTN.
072400
072500     MOVE INPATIENT-TREATMENT-REC-DATA TO WS-TRAILER-REC.
072600
072700     IF RECORDS-READ NOT EQUAL TO IN-RECORD-COUNT
072800         MOVE "** INVALID FILE - # RECORDS OUT OF BALANCE"
072900                               TO ABEND-REASON
073000         GO TO 1000-ABEND-RTN.
073100
073200
073300     IF WS-ANCILLARY-CHARGES NOT EQUAL TO IN-ANCILLARY-CHARGES
073400         MOVE "** ANCILLARY CHARGES OUT OF BALANCE"
073500                               TO ABEND-REASON
073600         MOVE WS-ANCILLARY-CHARGES TO EXPECTED-VAL
073700         MOVE IN-ANCILLARY-CHARGES
073800                TO ACTUAL-VAL
073900         DISPLAY "** ANCILLARY CHARGES IN **"
074000         DISPLAY WS-ANCILLARY-CHARGES
074100         DISPLAY "** ANCILLARY CHARGES EXPECTED **"
074200         DISPLAY  IN-ANCILLARY-CHARGES.
074300
074400     IF WS-MEDICATION-CHARGES  NOT EQUAL TO IN-MEDICATION-CHARGES
074500         MOVE "** MEDICATION CHARGES OUT OF BALANCE"
074600                               TO ABEND-REASON
074700         DISPLAY "** MEDICATION CHARGES IN **"
074800         DISPLAY WS-MEDICATION-CHARGES
074900         DISPLAY "** MEDICATION CHARGES EXPECTED **"
075000         DISPLAY  IN-MEDICATION-CHARGES.
075100
075200     IF WS-PHARMACY-CHARGES  NOT EQUAL TO IN-PHARMACY-CHARGES
075300         MOVE "** PHARMACY CHARGES OUT OF BALANCE"
075400                               TO ABEND-REASON
075500         DISPLAY "** PHARMACY CHARGES IN **"
075600         DISPLAY WS-PHARMACY-CHARGES
075700         DISPLAY "** PHARMACY CHARGES EXPECTED **"
075800         DISPLAY  IN-PHARMACY-CHARGES.
075900
076000     MOVE "T" TO RECORD-TYPE.
076100     ADD +1 TO RECORDS-WRITTEN.
076200     MOVE RECORDS-WRITTEN TO IN-RECORD-COUNT.
076300     MOVE WS-ANCILLARY-CHARGES TO IN-ANCILLARY-CHARGES.
076400     MOVE WS-MEDICATION-CHARGES TO IN-MEDICATION-CHARGES.
076500     MOVE WS-PHARMACY-CHARGES TO IN-PHARMACY-CHARGES.
076600     WRITE INPATIENT-TREATMENT-REC-EDIT FROM WS-TRAILER-REC.
076700
076800*  Code the statement to close all files
076900     PERFORM 850-CLOSE-FILES THRU 850-EXIT.
077000
077100     DISPLAY "** RECORDS READ **".
077200     DISPLAY RECORDS-READ.
077300     DISPLAY "** RECORD-IN EXPECTED **".
077400     DISPLAY  IN-RECORD-COUNT.
077500     DISPLAY "** RECORDS WRITTEN **".
077600     DISPLAY  RECORDS-WRITTEN.
077700     DISPLAY "** ERROR RECORDS FOUND **".
077800     DISPLAY  RECORDS-IN-ERROR.
077900
078000*  Code the statement to Display a successful end-of-job msg
078100     DISPLAY "******** NORMAL END OF JOB TRTMNT ********".
078200 999-EXIT.
078300     EXIT.
078400
078500 1000-ABEND-RTN.
078600     WRITE SYSOUT-REC FROM ABEND-REC.
078700     PERFORM 850-CLOSE-FILES THRU 850-EXIT.
078800     DISPLAY "*** ABNORMAL END OF JOB - TRTMNT ***" UPON CONSOLE.
078900     DIVIDE ZERO-VAL INTO ONE-VAL.
079000
079100 1000-DB2-ERROR-RTN.
079200************************************************************
079300*       ERROR TRAPPING ROUTINE FOR INVALID SQLCODES        *
079400************************************************************
079500
079600      DISPLAY '**** WE HAVE A SERIOUS PROBLEM HERE *****'.
079700      DISPLAY '999-ERROR-TRAP-RTN '.
079800      MULTIPLY SQLCODE BY -1 GIVING SQLCODE.
079900      DISPLAY 'SQLCODE ==> ' SQLCODE.
080000      DISPLAY SQLCA.
080100      DISPLAY SQLERRM.
080200      EXEC SQL WHENEVER SQLERROR CONTINUE END-EXEC.
080300      EXEC SQL ROLLBACK WORK END-EXEC.
080400      GO TO 1000-ABEND-RTN.